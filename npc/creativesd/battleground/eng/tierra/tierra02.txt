/*.-----------------------------------------------------------------.
  .    ____                          __                             .
  .   /\  _`\                       /\ \__  __                      .
  .   \ \ \/\_\  _ __    __     __  \ \ ,_\/\_\  __  __     __      .
  .    \ \ \/_/_/\`'__\/'__`\ /'__`\ \ \ \/\/\ \/\ \/\ \  /'__`\    .
  .     \ \ \s\ \ \ \//\  __//\ \d\.\_\ \ \_\ \ \ \ \_/ |/\  __/    .
  .      \ \____/\ \_\\ \____\ \__/.\_\\ \__\\ \_\ \___/ \ \____\   .
  .       \/___/  \/_/ \/____/\/__/\/_/ \/__/ \/_/\/__/   \/____/   .
  .                                                                 .
  .          2014~2017 © Creative Services and Development          .
  .                      www.creativesd.com.br                      .
  .-----------------------------------------------------------------.
  . Script:                                                         .
  .    Battleground Warfare (2nd Tierra Gorge)                      .
  .-----------------------------------------------------------------.
  . Author: Romulo SM (sbk_)                         Version: 1.0   .
  .                                                                 .
  . Compatible:                                                     .
  .    eAthena, rAthena and derivations.                            .
  .-----------------------------------------------------------------.
  . Description:                                                    .
  .    Battle in which players must destroy the opponent's crystal. .                                                   .
  *-----------------------------------------------------------------*/
  
// Tierra Gorge Engine
// ----------------------------------------------------------------
bat_a02,15,15,3	script	start#bat_a02	CLEAR_NPC,{
	end;
	
OnInit:
	// Removing Players from Arena
	mapwarp "bat_a02","bat_room",154,150;
	
	// Creating Queue and specific times for Arena
	queue_create(2, "2nd Tierra Gorge", 0, 0, "start#bat_a02::OnReadyCheck");
	set $@TierraBG2_id1, bg_create_team(3, "bat_a02", 50, 374, 352, 342, 0, "start#bat_a02::OnGuillaumeQuit", "", "start#bat_a02::OnGuillWO");
	set $@TierraBG2_id2, bg_create_team(4, "bat_a02", 42, 16, 353, 52, 1, "start#bat_a02::OnCroixQuit", "", "start#bat_a02::OnCroixWO");
	
	// Expeling Players
	bg_kick_team($@TierraBG2_id1, 0);
	bg_kick_team($@TierraBG2_id2, 0);
	
	// Getting Started
	set .global_announce, 0;
	set .BlockStatus, 0;
	donpcevent "start#bat_a02::OnReadyCheck";
	
	// Set NPC's of Flags with the Symbol of the Army.
	donpcevent "::OnBatA02Emblem";
	end;

OnReadyCheck:
	if( .BlockStatus )
	{
		sleep 5000;
		donpcevent "start#bat_a02::OnReadyCheck";
	}
		
	set .BlockStatus, 1;

	set .@min_player, callfunc("bg_getarenainfo", 2, "min_player");
	set .@max_player, callfunc("bg_getarenainfo", 2, "max_player");
	set .@a_name$, callfunc("bg_getarenainfo", 2, "arena_name");
	set .@min_player, (.@min_player*2);
	
	if( .@max_player > MAX_BG_MEMBERS )
	{
		bg_console "(BG_ID: 2) Maximum number of players in a team exceeds the established limit, setting the maximum number of players to " + MAX_BG_MEMBERS + ".";
		set .@max_player, MAX_BG_MEMBERS;
	}

	queue_get_players 2;
	set .@playlist_team1, bg_team_size($@TierraBG2_id1);
	set .@playlist_team2, bg_team_size($@TierraBG2_id2);
	
	if( !.@queue_count )
	{
		set .BlockStatus, 0;
		end;
	}
	else if( $@TierraBG2 && $@TierraBG2 < 2 && .@queue_count && (.@playlist_team1 < .@max_player || .@playlist_team2 < .@max_player) )
	{
		set .@guill, 0;
		set .@croix, 0;
		set .@count_g, 0;
		set .@count_c, 0;
		
		// ---------------------------------------------------
		// -              Equating the Armies                -
		// ---------------------------------------------------
		if( .@playlist_team1 < .@playlist_team2 )
		{
			set .@guill, .@playlist_team2-.@playlist_team1;
			for( set .@i,0; .@i < .@guill; set .@i, .@i + 1 )
			{
				bg_join_team $@TierraBG2_id1, 1, .@queue_aid[.@i];
				queue_leave 0, .@queue_aid[.@i];
				set .@count_g, .@count_g + 1;
				message .@queue_player$[.@i], "You joined Guillaume's Army in a battle on " + .@a_name$ + " that is already happening, and will be teleported in 10 seconds.";
			}
			
			deletearray .@queue_cid;
			deletearray .@queue_aid;
			deletearray .@queue_player$;
			set .@queue_count, 0;
			queue_get_players 2; // Reset
			set .@playlist_team1, bg_team_size($@TierraBG2_id1);
		}
		else if( .@playlist_team2 < .@playlist_team1 )
		{
			set .@croix, .@playlist_team1-.@playlist_team2;
			for( set .@i,0; .@i < .@croix; set .@i, .@i + 1 )
			{
				bg_join_team $@TierraBG2_id1, 1, .@queue_aid[.@i];
				queue_leave 0, .@queue_aid[.@i];
				set .@count_c, .@count_c + 1;
				message .@queue_player$[.@i], "You joined Croix's Army in a battle on" + .@a_name$ + " that is already happening, and will be teleported in 10 seconds.";
			}
			
			deletearray .@queue_cid;
			deletearray .@queue_aid;
			deletearray .@queue_player$;
			set .@queue_count, 0;
			queue_get_players 2; // Reset
			set .@playlist_team2, bg_team_size($@TierraBG2_id2);
		}
		// ---------------------------------------------------
		// -             Expanding the Armies                -
		// ---------------------------------------------------
		if( .@queue_count && .@playlist_team1 == .@playlist_team2 && .@playlist_team1 < .@max_player && .@playlist_team2 < .@max_player )
		{
			set .@guill, .@max_player-.@playlist_team1;
			set .@croix, .@max_player-.@playlist_team2;
			
			if( .@queue_count > (.@guill+.@croix) )
				set .@queue_count, (.@guill+.@croix);
				
			if( .@queue_count%2 )
				set .@queue_count, .@queue_count-1;
			
			set .@size, .@queue_count/2;
			for( set .@i, 0; .@i < .@size; set .@i, .@i + 1 )
			{
				bg_join_team $@TierraBG2_id1, 1, .@queue_aid[.@i];
				queue_leave 0, .@queue_aid[.@i];
				set .@players_name_g$[.@count_g], .@queue_player$[.@i];
				set .@count_g, .@count_g + 1;
			}
			
			for( set .@i,.@size; .@i < .@queue_count; set .@i, .@i + 1 )
			{
				bg_join_team $@TierraBG2_id2, 1, .@queue_aid[.@i];
				queue_leave 0, .@queue_aid[.@i];
				set .@players_name_c$[.@count_c], .@queue_player$[.@i];
				set .@count_c, .@count_c + 1;
			}
		}
		
		if( .@count_c || .@count_g )
		{
			announce "[Battleground]: " + (.@count_c+.@count_g) + " players are being sent to a battle that is going on in " + .@a_name$ + ", the Armies were balanced and expanded.", bc_all, "0xFFCE00";
			sleep 3000;
		}
		
		deletearray .@queue_cid;
		deletearray .@queue_aid;
		deletearray .@queue_player$;
		set .@queue_count, 0;
		queue_get_players 2; // Reset
		
		set .@queue_size, queue_size(2);
		set .@team1_size, bg_team_size($@TierraBG2_id1);
		set .@team2_size, bg_team_size($@TierraBG2_id2);
		
		if( .@team1_size < .@max_player || .@team2_size < .@max_player )
		{
			set .@size, (2*.@max_player)-(.@team1_size+.@team2_size)-.@queue_size;
			announce "[Battleground]: There are still " + (.@size) + " spots for a battle going on in " + .@a_name$ + "!", bc_all, "0xFFCE00";
		}
	}
	else if( $@TierraBG2 && !.global_announce && .@queue_count >= .@min_player )
	{
		announce "[Battleground]: The battle is complete, but there is a battle going on in " + .@a_name$ + ", all players standing in the queue must wait.", bc_all, "0xFFCE00";
		set .global_announce, 1;
	}
	else if( .@queue_count < .@min_player )
		announce "[Battleground]: There are " + (.@min_player-.@queue_count) + " players left in the battle in " + .@a_name$ + "!", bc_all, "0xFFCE00";
	else
	{
		// ---------------------------------------------------
		// -                    Balancing                    -
		// ---------------------------------------------------
		if( .@queue_count > (.@max_player*2) )
			set .@queue_count, (.@max_player*2);
			
		if( .@queue_count%2 )
			set .@queue_count, .@queue_count-1;
			
		if( .@queue_count >= .@min_player )
		{
			for( set .@i,0; .@i < .@queue_count; set .@i, .@i + 2 )
			{
				// Entering Guillaume's Army
				bg_join_team $@TierraBG2_id1, 1, .@queue_aid[.@i];
				queue_leave 0, .@queue_aid[.@i];
				message .@queue_player$[.@i], "You joined Guillaume's Army, you will be teleported in 10 seconds.";
				
				// Entering Croix's Army
				bg_join_team $@TierraBG2_id2, 1, .@queue_aid[.@i+1];
				queue_leave 0, .@queue_aid[.@i+1];
				message .@queue_player$[.@i+1], "You joined the Croix's Army, you will be teleported in 10 seconds.";
			}
		}

		bg_refresh_patent $@TierraBG2_id1;
		bg_refresh_patent $@TierraBG2_id2;
		set .global_announce, 0;
		set $@TierraBG2, 1;
		announce "[Battleground]: Both Armies, are complete good luck in the battles at " + .@a_name$ + ".", bc_all, "0xFFCE00";
		set $@TierraBG2_CroixScore, 0;
		set $@TierraBG2_GuillScore, 0;
		bg_updatescore "bat_a02",0,0;
		donpcevent "start#bat_a02::OnEnable";
		donpcevent "BattleGround#queue_main::OnStopTimer";
		sleep 3000;
		initnpctimer;
		
		if( .@queue_count < (2*.@max_player) )
		{
			sleep 3000;
			set .@count, (2*.@max_player)-.@queue_count;
			announce "[Battleground]: There are still " + .@queue_count + " spots in the battles in " + .@a_name$ + "!", bc_all, "0xFFCE00";
		}
	}
	set .BlockStatus, 0;
	end;
	
OnEnable:
	bg_set_respawn $@TierraBG2_id1, 352, 342;
	bg_set_respawn $@TierraBG2_id2, 353, 52;
	donpcevent "OBJ#bat_a02_a::OnKill";
	donpcevent "OBJ#bat_a02_a::OnEnable";
	donpcevent "OBJ#bat_a02_b::OnKill";
	donpcevent "OBJ#bat_a02_b::OnEnable";
	donpcevent "barricade#bat_a02_a::OnKill";
	donpcevent "barricade#bat_a02_a::OnEnable";
	donpcevent "barricade#bat_a02_b::OnKill";
	donpcevent "barricade#bat_a02_b::OnEnable";
	donpcevent "OBJ#bat_a02_n::OnKill";
	donpcevent "OBJ#bat_a02_n::OnEnable";
	donpcevent "NOBJ_mob#bat_a02_a::OnKill";
	donpcevent "NOBJ_mob#bat_a02_b::OnKill";
	donpcevent "Battle Therapist#a02_a::OnEnable";
	donpcevent "Battle Therapist#a02_b::OnEnable";
	donpcevent "countdown#bat_a02::OnEnable";
	donpcevent "time_event#bat_a02::OnEnable";
	disablenpc "Guillaume Blacksmith#a02";
	disablenpc "Croix Blacksmith#bat_a02";
	disablenpc "Lieutenant#a02_a";
	disablenpc "Lieutenant#a02_b";
	end;

OnGuillaumeQuit:
	callfunc "bg_clear_score", getcharid(0);
	callfunc "bg_ranking", getcharid(0), 2, 0, 0, 1, 0, $@TierraBG2_CroixScore;
	donpcevent "start#bat_a02::OnReadyCheck";
	end;
	
OnCroixQuit:
	callfunc "bg_clear_score", getcharid(0);
	callfunc "bg_ranking", getcharid(0), 2, 0, 1, 1, $@TierraBG2_GuillScore, 0;
	donpcevent "start#bat_a02::OnReadyCheck";
	end;

OnGuillWO:
	if( queue_size(2) )
		end;
	
	// Sign the Victory
	set $@TierraBG2, 2;
	set $@TierraBG2_Victory, 2;
	
	// Computes Ranking and awards rewards
	callfunc "bg_finish", 2, $@TierraBG2_id1, $@TierraBG2_id2, $@TierraBG2_Victory, $@TierraBG2_GuillScore, $@TierraBG2_CroixScore;
	
	// Starts Ending Events
	donpcevent "Battle Therapist#a02_a::OnStop";
	donpcevent "Battle Therapist#a02_b::OnStop";
	donpcevent "countdown#bat_a02::OnStop";
	donpcevent "time_event#bat_a02::OnStop";
	donpcevent "start#bat_a02_Out::OnBegin";
	mapannounce "bat_a02", "Lieutenant Swandery: Guillaume's cowards fled. We won this one! Wow!",bc_map,"0xFFCE00";
	enablenpc "Lieutenant#a02_a";
	enablenpc "Lieutenant#a02_b";
	sleep 2000;
	bg_warp $@TierraBG2_id1,"bat_a02",50,374;
	bg_warp $@TierraBG2_id2,"bat_a02",42,16;
	end;
	
OnCroixWO:
	if( queue_size(2) )
		end;
	
	// Sign the Victory
	set $@TierraBG2, 2;
	set $@TierraBG2_Victory, 1;
	
	// Computes Ranking and awards rewards
	callfunc "bg_finish", 2, $@TierraBG2_id1, $@TierraBG2_id2, $@TierraBG2_Victory, $@TierraBG2_GuillScore, $@TierraBG2_CroixScore;
	
	// Starts Ending Events
	donpcevent "Battle Therapist#a02_a::OnStop";
	donpcevent "Battle Therapist#a02_b::OnStop";
	donpcevent "countdown#bat_a02::OnStop";
	donpcevent "time_event#bat_a02::OnStop";
	donpcevent "start#bat_a02_Out::OnBegin";
	mapannounce "bat_a02", "Lieutenant Guillaume Axl Rose: Croix's cowards fled. We won this one! Wow!",bc_map,"0xFFCE00";
	enablenpc "Lieutenant#a02_a";
	enablenpc "Lieutenant#a02_b";
	sleep 2000;
	bg_warp $@TierraBG2_id1,"bat_a02",50,374;
	bg_warp $@TierraBG2_id2,"bat_a02",42,16;
	end;

// Player Check in the Arena.
// -----------------------------------------------------------------
OnTimer60000:
	stopnpctimer;
	if( !getmapusers("bat_a02") )
	{
		set $@TierraBG2, 0;
		bg_kick_team $@TierraBG2_id1;
		bg_kick_team $@TierraBG2_id2;
		donpcevent "start#bat_a02_Out::OnStop";
		
		if( $@BG_MODE )
			donpcevent "BattleGround#queue_main::OnReload";
		else
			donpcevent "start#bat_a02::OnReadyCheck";
	}
	else
		initnpctimer;
	end;
}

bat_a02,15,16,3	script	OBJ#bat_a02_a	CLEAR_NPC,{
OnEnable:
	bg_monster $@TierraBG2_id1,"bat_a02",176,345,"Food Storage",1909,"","OBJ#bat_a02_a::OnMyMobDead";
	end;

OnKill:
	killmonster "bat_a02","OBJ#bat_a02_a::OnMyMobDead";
	end;

OnMyMobDead:
	if (mobcount("bat_a02","OBJ#bat_a02_a::OnMyMobDead") < 1) {
		set $@TierraBG2_CroixScore, $@TierraBG2_CroixScore+1;
		set .@score_round, callfunc("bg_getarenainfo",2,"score_round");
		bg_updatescore "bat_a02", $@TierraBG2_GuillScore, $@TierraBG2_CroixScore;
		
		if( $@TierraBG2_CroixScore >= .@score_round )
		{
			set $@TierraBG2, 2;
			set $@TierraBG2_Victory, 2;
			donpcevent "Battle Therapist#a02_a::OnStop";
			donpcevent "Battle Therapist#a02_b::OnStop";
			donpcevent "countdown#bat_a02::OnStop";
			donpcevent "time_event#bat_a02::OnStop";
			enablenpc "Lieutenant#a02_a";
			enablenpc "Lieutenant#a02_b";
			donpcevent "start#bat_a02_Out::OnBegin";
			mapannounce "bat_a02", "Lieutenant Swandery: We destroyed Guillaume's Army Food Storage. We won that! Wow!",bc_map,"0xFFCE00";
			
			// Computes Ranking and awards rewards
			callfunc "bg_finish", 2, $@TierraBG2_id1, $@TierraBG2_id2, $@TierraBG2_Victory, $@TierraBG2_GuillScore, $@TierraBG2_CroixScore;
			
			bg_warp $@TierraBG2_id1,"bat_a02",50,374;
			bg_warp $@TierraBG2_id2,"bat_a02",42,16;
		}
		else {
			donpcevent "start#bat_a02::OnEnable";
			bg_warp $@TierraBG2_id1,"bat_a02",352,342;
			bg_warp $@TierraBG2_id2,"bat_a02",353,52;
			sleep 1000;
			mapannounce "bat_a02", "Lieutenant Swandery: We destroyed Guillaume's Army Food Storage. We won this round!",bc_map,"0xFFCE00";
		}
	}
	end;
}

bat_a02,15,17,3	script	OBJ#bat_a02_b	CLEAR_NPC,{
OnEnable:
	bg_monster $@TierraBG2_id2,"bat_a02",167,50,"Food Storage",1910,"","OBJ#bat_a02_b::OnMyMobDead";
	end;

OnKill:
	killmonster "bat_a02","OBJ#bat_a02_b::OnMyMobDead";
	end;

OnMyMobDead:
	if (mobcount("bat_a02","OBJ#bat_a02_b::OnMyMobDead") < 1) {
		set $@TierraBG2_GuillScore, $@TierraBG2_GuillScore+1;
		set .@score_round, callfunc("bg_getarenainfo",2,"score_round");
		bg_updatescore "bat_a02", $@TierraBG2_GuillScore, $@TierraBG2_CroixScore;
		if( $@TierraBG2_GuillScore >= .@score_round )
		{
			set $@TierraBG2, 2;
			set $@TierraBG2_Victory, 1;
			donpcevent "Battle Therapist#a02_a::OnStop";
			donpcevent "Battle Therapist#a02_b::OnStop";
			donpcevent "countdown#bat_a02::OnStop";
			donpcevent "time_event#bat_a02::OnStop";
			enablenpc "Lieutenant#a02_a";
			enablenpc "Lieutenant#a02_b";
			donpcevent "start#bat_a02_Out::OnBegin";
			mapannounce "bat_a02", "Lieutenant Axl Rose : We destroyed Croix's Army Food Storage. We won that! Wow!",bc_map,"0xFFCE00";
			
			// Computes Ranking and awards rewards
			callfunc "bg_finish", 2, $@TierraBG2_id1, $@TierraBG2_id2, $@TierraBG2_Victory, $@TierraBG2_GuillScore, $@TierraBG2_CroixScore;
			
			bg_warp $@TierraBG2_id1,"bat_a02",50,374;
			bg_warp $@TierraBG2_id2,"bat_a02",42,16;
		}
		else {
			donpcevent "start#bat_a02::OnEnable";
			bg_warp $@TierraBG2_id1,"bat_a02",352,342;
			bg_warp $@TierraBG2_id2,"bat_a02",353,52;
			sleep 1000;
			mapannounce "bat_a02", "Lieutenant Swandery: We destroyed Croix's Army Food Storage. We won this round!",bc_map,"0xFFCE00";
		}
	}
	end;
}

bat_a02,15,18,3	script	barricade#bat_a02_a	CLEAR_NPC,{
OnEnable:
	for ( set .@i,185; .@i < 202; set .@i,.@i+1 )
		bg_monster $@TierraBG2_id1,"bat_a02",.@i,266,"Barricade",1906,"","barricade#bat_a02_a::OnMyMobDead";
	setwall "bat_a02",186,266,16,6,1,"bat_a02_c1";
	end;

OnKill:
	killmonster "bat_a02","barricade#bat_a02_a::OnMyMobDead";
	delwall "bat_a02_c1";
	end;

OnMyMobDead:
	if (mobcount("bat_a02","barricade#bat_a02_a::OnMyMobDead") < 17) {
		killmonster "bat_a02","barricade#bat_a02_a::OnMyMobDead";
		delwall "bat_a02_c1";
		enablenpc "Guillaume Blacksmith#a02";
		mapannounce "bat_a02", "Lieutenant Axl Rose : The Barricade in the valley has been destroyed! Where's the Blacksmith? We need to rebuild the Barricade!",bc_map,"0xFFCE00";
	}
	end;
}

bat_a02,15,19,3	script	barricade#bat_a02_b	CLEAR_NPC,{
OnEnable:
	for ( set .@i,169; .@i < 186; set .@i,.@i+1 )
		bg_monster $@TierraBG2_id2,"bat_a02",.@i,129,"Barricade",1906,"","barricade#bat_a02_b::OnMyMobDead";
	setwall "bat_a02",170,129,16,6,1,"bat_a02_g1";
	end;

OnKill:
	killmonster "bat_a02","barricade#bat_a02_b::OnMyMobDead";
	delwall "bat_a02_g1";
	end;

OnMyMobDead:
	if (mobcount("bat_a02","barricade#bat_a02_b::OnMyMobDead") < 17) {
		killmonster "bat_a02","barricade#bat_a02_b::OnMyMobDead";
		delwall "bat_a02_g1";
		enablenpc "Croix Blacksmith#bat_a02";
		mapannounce "bat_a02", "Lieutenant Swandery : The Barricade in the valley has been destroyed! Where's the Blacksmith? We need to rebuild the Barricade!",bc_map,"0xFFCE00";
	}
	end;
}

bat_a02,15,20,3	script	OBJ#bat_a02_n	CLEAR_NPC,{
OnEnable:
	monster "bat_a02",273,203,"Neutrality Flag",1911,1,"OBJ#bat_a02_n::OnMyMobDead";
	end;

OnKill:
	killmonster "bat_a02","OBJ#bat_a02_n::OnMyMobDead";
	end;

OnMyMobDead:
	if (mobcount("bat_a02","OBJ#bat_a02_n::OnMyMobDead") < 17) {
		bg_set_respawn getcharid(4),301,209;
		if (getcharid(4) == $@TierraBG2_id1) {
			donpcevent "NOBJ_mob#bat_a02_a::OnEnable";
			mapannounce "bat_a02", "Guillaume captured a Neutrality Flag, so they have an advantage.",bc_map,"0xFFCE00";
		}
		else {
			donpcevent "NOBJ_mob#bat_a02_b::OnEnable";
			mapannounce "bat_a02", "Croix captured a Neutrality Flag, so they have an advantage.",bc_map,"0xFFCE00";
		}
	}
	end;
}

bat_a02,15,21,3	script	NOBJ_mob#bat_a02_a	CLEAR_NPC,{
OnEnable:
	donpcevent "NOBJ_mob#bat_a02_b::OnKill";
	bg_monster $@TierraBG2_id1,"bat_a02",272,204,"Guardian",1949,"Camp Guard","NOBJ_mob#bat_a02_a::OnMyMobDead";
	bg_monster $@TierraBG2_id1,"bat_a02",272,213,"Guardian",1949,"Camp Guard","NOBJ_mob#bat_a02_a::OnMyMobDead";
	bg_monster $@TierraBG2_id1,"bat_a02",273,197,"Guardian",1950,"Camp Guard","NOBJ_mob#bat_a02_a::OnMyMobDead";
	end;

OnKill:
	killmonster "bat_a02","NOBJ_mob#bat_a02_a::OnMyMobDead";
	end;

OnMyMobDead:
	end;
}

bat_a02,15,22,3	script	NOBJ_mob#bat_a02_b	CLEAR_NPC,{
OnEnable:
	donpcevent "NOBJ_mob#bat_a02_a::OnKill";
	bg_monster $@TierraBG2_id2,"bat_a02",272,204,"Guardian",1949,"Camp Guard","NOBJ_mob#bat_a02_a::OnMyMobDead";
	bg_monster $@TierraBG2_id2,"bat_a02",272,213,"Guardian",1949,"Camp Guard","NOBJ_mob#bat_a02_a::OnMyMobDead";
	bg_monster $@TierraBG2_id2,"bat_a02",273,197,"Guardian",1950,"Camp Guard","NOBJ_mob#bat_a02_a::OnMyMobDead";
	end;

OnKill:
	killmonster "bat_a02","NOBJ_mob#bat_a02_b::OnMyMobDead";
	end;

OnMyMobDead:
	end;
}

bat_a02,185,270,1	script	Guillaume Blacksmith#a02	4_M_REPAIR,{
	if (getcharid(4) == $@TierraBG2_id1) {
		mes "[Guillaume Blacksmith]";
		mes "We are in urgency! The Barricade has been destroyed!";
		mes "We can repair the Barricade with ^3131FF50 Stones, 3 Sinew of Bear, 500 Metal Fragments, 30 Rough Elunium and 100 Gold.^000000";
		mes "We have it all except for the 50 Stones!";
		next;
		switch(select("Repair.:Leave it.")) {
		case 1:
			if (countitem(7049) > 49) {
				mes "[Guillaume Blacksmith]";
				mes "You brought enough stones! Let's go and repair.";
				next;
				mes "..";
				next;
				mes "....";
				next;
				mes "......";
				next;
				mes "........";
				next;
				mes "..........";
				next;
				mes "............";
				next;
				mes "..............";
				next;
				mes "[Guillaume Blacksmith]";
				mes "Combine Stones and Gold in the proper percentage.";
				next;
				mes "[Guillaume Blacksmith]";
				mes "Combine Stones and Gold in the proper percentage and shape the Barricade.";
				next;
				mes "[Guillaume Blacksmith]";
				mes "Combine Stones and Gold in the proper percentage and shape the Barricade, then add Rough Elunium to make it stronger.";
				next;
				mes "[Guillaume Blacksmith]";
				mes "Combine Stones and Gold in the proper percentage and shape the Barricade, then add Rough Elunium to make it stronger. Now decorate with Metal Fragments.";
				next;
				mes "[Guillaume Blacksmith]";
				mes "Combine Stones and Gold in the proper percentage and shape the Barricade, then add Rough Elunium to make it stronger. Decorate with Metal Fragments, and plait stones with Sinew of Bear!";
				next;
				mes "..............";
				mes "..............";
				mes "..............";
				mes "..............";
				mes "..............";
				next;
				specialeffect EF_REPAIRWEAPON;
				mes "[Guillaume Blacksmith]";
				mes "Wow! It's done.";
				mes "We are relieved.";
				delitem 7049,50; //Stone
				set .@score, bg_get_score(31);
				bg_set_score 31, (.@score+16); // Set Score
				donpcevent "barricade#bat_a02_a::OnEnable";
				close2;
				disablenpc "Guillaume Blacksmith#a02";
				end;
			}
			else {
				mes "[Guillaume Blacksmith]";
				mes "You don't have enough Stones!";
				next;
				mes "[Guillaume Blacksmith]";
				mes "^3131FFWe need 50 Stones.^000000";
				mes "We are busy, so please hurry.";
				close;
			}
		case 2:
			mes "[Guillaume Blacksmith]";
			mes "There are enemies coming! Let's evacuate from here!";
			close;
		}
	}
	else {
		mes "[Guillaume Blacksmith]";
		mes "There the enemy is coming!";
		close;
	}

OnInit:
	disablenpc "Guillaume Blacksmith#a02";
	end;
}

bat_a02,170,121,5	script	Croix Blacksmith#bat_a02	4_M_REPAIR,{
	if (getcharid(4) == $@TierraBG2_id2) {
		mes "[Croix Blacksmith]";
		mes "We are in urgency! The Barricade has been destroyed!";
		mes "We can repair the Barricade with ^3131FF50 Stones, 3 Sinew of Bear, 500 Metal Fragments, 30 Rough Elunium and 100 Gold.^000000";
		mes "We have it all except for the 50 Stones!";
		next;
		switch(select("Repair.:Leave it.")) {
		case 1:
			if (countitem(7049) > 49) {
				mes "[Croix Blacksmith]";
				mes "You brought enough stones! Let's go and repair.";
				next;
				mes "..";
				next;
				mes "....";
				next;
				mes "......";
				next;
				mes "........";
				next;
				mes "..........";
				next;
				mes "............";
				next;
				mes "..............";
				next;
				mes "[Croix Blacksmith]";
				mes "Combine Stones and Gold in the proper percentage.";
				next;
				mes "[Croix Blacksmith]";
				mes "Combine Stones and Gold in the proper percentage and shape the Barricade.";
				next;
				mes "[Croix Blacksmith]";
				mes "Combine Stones and Gold in the proper percentage and shape the Barricade, then add Rough Elunium to make it stronger.";
				next;
				mes "[Croix Blacksmith]";
				mes "Combine Stones and Gold in the proper percentage and shape the Barricade, then add Rough Elunium to make it stronger. Now decorate with Metal Fragments.";
				next;
				mes "[Croix Blacksmith]";
				mes "Combine Stones and Gold in the proper percentage and shape the Barricade, then add Rough Elunium to make it stronger. Decorate with Metal Fragments, and plait stones with Sinew of Bear!";
				next;
				mes "..............";
				mes "..............";
				mes "..............";
				mes "..............";
				mes "..............";
				next;
				specialeffect EF_REPAIRWEAPON;
				mes "[Croix Blacksmith]";
				mes "Wow! It's done.";
				mes "We are relieved.";
				delitem 7049,50; //Stone
				set .@score, bg_get_score(30);
				bg_set_score 30, (.@score+16); // Set Score
				donpcevent "barricade#bat_a02_b::OnEnable";
				close2;
				disablenpc "Croix Blacksmith#bat_a02";
				end;
			}
			else {
				mes "[Croix Blacksmith]";
				mes "You don't have enough Stones!";
				next;
				mes "[Croix Blacksmith]";
				mes "^3131FFWe need 50 Stone.^000000";
				mes "We are busy, so please hurry.";
				close;
			}
		case 2:
			mes "[Croix Blacksmith]";
			mes "There are enemies coming! Let's evacuate from here!";
			close;
		}
	}
	else {
		mes "[Croix Blacksmith]";
		mes "There, the enemy is coming!";
		close;
	}

OnInit:
	disablenpc "Croix Blacksmith#bat_a02";
	end;
}

bat_a02,53,377,3	script	Battle Therapist#a02_a	4_F_SISTER,{
	specialeffect2 EF_HEAL;
	mes "[Battle Therapist]";
	mes "Just close your eyes";
	mes "and take a deep breath.";
	mes "You can free yourself from pain.";
	close;

OnInit:
	initnpctimer;
	end;

OnEnable:
	stopnpctimer;
	initnpctimer;
	enablenpc "Battle Therapist#a02_a";
	end;

OnStop:
	disablenpc "Battle Therapist#a02_a";
	stopnpctimer;
	end;

OnTimer25000:
	specialeffect EF_SANCTUARY;
	end;

OnTimer26500:
	donpcevent "Battle Therapist#a02_a::OnEnable";
	end;
}

bat_a02,45,19,3	script	Battle Therapist#a02_b	4_F_SISTER,{
	specialeffect2 EF_HEAL;
	mes "[Battle Therapist]";
	mes "Just close your eyes";
	mes "and take a deep breath.";
	mes "You can free yourself from pain.";
	close;

OnInit:
	initnpctimer;
	end;

OnEnable:
	stopnpctimer;
	initnpctimer;
	enablenpc "Battle Therapist#a02_b";
	end;

OnStop:
	disablenpc "Battle Therapist#a02_b";
	stopnpctimer;
	end;

OnTimer25000:
	specialeffect EF_SANCTUARY;
	end;

OnTimer26500:
	donpcevent "Battle Therapist#a02_b::OnEnable";
	end;
}

bat_a02,60,216,3	script	Valley Ghost#bat_a02_n	4_GHOSTRING,{
	specialeffect2 EF_HEAL;
	mes "[Valley Ghost]";
	mes "Boo...Boo...";
	close;

OnInit:
	initnpctimer;
	end;

OnEnable:
	stopnpctimer;
	initnpctimer;
	end;

OnTimer25000:
	specialeffect EF_SANCTUARY;
	enablenpc "bat_a02_rp1_n_warp";
	end;
	
OnTimer26000:
	disablenpc "bat_a02_rp1_n_warp";
	end;

OnTimer26500:
	donpcevent "Valley Ghost#bat_a02_n::OnEnable";
	end;
}

bat_a02,55,213,0	script	bat_a02_rp1_n_warp	FAKE_NPC,10,10,{
OnInit:
	disablenpc "bat_a02_rp1_n_warp";
	end;

OnTouch:
	percentheal 100,100;
	warp "bat_a02",301,209;
	end;
}

bat_a02,194,267,0	script	barri_warp_up#bat_a02_a	FAKE_NPC,7,0,{
OnTouch:
	if (getcharid(4) == $@TierraBG2_id1)
		warp "bat_a02",194,261;
	end;
}

bat_a02,194,265,0	script	barri_warp_down#bat_a02a	FAKE_NPC,7,0,{
OnTouch:
	if (getcharid(4) == $@TierraBG2_id1)
		warp "bat_a02",194,270;
	end;
}

bat_a02,177,130,0	script	barri_warp_up#bat_a02_b	FAKE_NPC,7,0,{
OnTouch:
	if (getcharid(4) == $@TierraBG2_id2)
		warp "bat_a02",178,125;
	end;
}

bat_a02,177,128,0	script	barri_warp_down#bat_a02b	FAKE_NPC,7,0,{
OnTouch:
	if (getcharid(4) == $@TierraBG2_id2)
		warp "bat_a02",178,134;
	end;
}

bat_a02,169,227,0	script	underladd#bat_a02_1	WARPNPC,1,1,{
OnTouch_:
	warp "bat_a02",178,228;
	end;
}

bat_a02,208,164,0	script	underladd#bat_a02_2	WARPNPC,1,1,{
OnTouch_:
	warp "bat_a02",200,171;
	end;
}

// Guillaume flags
bat_a02,171,309,4	script	Guillaume Camp#flag11	GUILD_FLAG,{
	end;

OnBatA02Emblem:
	bg_set_npc $@TierraBG2_id1;
	end;
}

bat_a02,149,310,4	duplicate(Guillaume Camp#flag11)	Guillaume Camp#flag12	GUILD_FLAG
bat_a02,119,336,2	duplicate(Guillaume Camp#flag11)	Guillaume Camp#flag13	GUILD_FLAG
bat_a02,118,357,2	duplicate(Guillaume Camp#flag11)	Guillaume Camp#flag14	GUILD_FLAG
bat_a02,150,380,0	duplicate(Guillaume Camp#flag11)	Guillaume Camp#flag15	GUILD_FLAG
bat_a02,173,380,0	duplicate(Guillaume Camp#flag11)	Guillaume Camp#flag16	GUILD_FLAG
bat_a02,210,344,6	duplicate(Guillaume Camp#flag11)	Guillaume Camp#flag17	GUILD_FLAG
bat_a02,350,325,4	duplicate(Guillaume Camp#flag11)	Guillaume Camp#flag18	GUILD_FLAG
bat_a02,358,325,4	duplicate(Guillaume Camp#flag11)	Guillaume Camp#flag19	GUILD_FLAG

// Croix flags
bat_a02,138,12,4	script	Croix Camp#flag11	GUILD_FLAG,{
	end;

OnBatA02Emblem:
	bg_set_npc $@TierraBG2_id2;
	end;
}

bat_a02,108,36,2	duplicate(Croix Camp#flag11)	Croix Camp#flag12	GUILD_FLAG
bat_a02,108,63,2	duplicate(Croix Camp#flag11)	Croix Camp#flag13	GUILD_FLAG
bat_a02,136,87,0	duplicate(Croix Camp#flag11)	Croix Camp#flag14	GUILD_FLAG
bat_a02,167,86,0	duplicate(Croix Camp#flag11)	Croix Camp#flag15	GUILD_FLAG
bat_a02,199,49,5	duplicate(Croix Camp#flag11)	Croix Camp#flag16	GUILD_FLAG
bat_a02,168,16,4	duplicate(Croix Camp#flag11)	Croix Camp#flag17	GUILD_FLAG
bat_a02,357,74,0	duplicate(Croix Camp#flag11)	Croix Camp#flag18	GUILD_FLAG
bat_a02,348,74,0	duplicate(Croix Camp#flag11)	Croix Camp#flag19	GUILD_FLAG

bat_a02,1,5,3	script	countdown#bat_a02	CLEAR_NPC,{
OnInit:
	stopnpctimer;
	end;

OnEnable:
	stopnpctimer;
	initnpctimer;
	end;

OnStop:
	stopnpctimer;
	end;

OnTimer7000:
	mapannounce "bat_a02", "Tenente Axl Rose : Let's attack to burn down Croix's Food Depot!",bc_map,"0xFF9900";
	end;

OnTimer8000:
	mapannounce "bat_a02", "Lieutenant Swandery : Master of Valhalla! Let us be gifted with unfailing faith and courage!",bc_map,"0xFF99CC";
	end;

OnTimer1800000:
	mapannounce "bat_a02", "Marollo VII : Guillaume Marollo, Croix Marollo! Marollo followers!",bc_map,"0x99CC00";
	end;

OnTimer1803000:
	mapannounce "bat_a02", "Marollo VII : Both camps are competitive, so no camp would be destroyed easily. That means the Marollo kingdoms will never be defeated!",bc_map,"0x99CC00";
	end;

OnTimer1808000:
	mapannounce "bat_a02", "Marollo VII : I think we'd better terminate the battle, and call it a draw.",bc_map,"0x99CC00";
	end;

OnTimer1822000:
	mapannounce "bat_a02", "Marollo VII : Hold your royalty and faith for a moment, and let's settle up the battle of Tierra Gorge.",bc_map,"0x99CC00";
	end;

OnTimer1825000:
	mapannounce "bat_a02", "Axl Rose, Swandery : Yes sir.",bc_map,"0x99CC00";
	end;

OnTimer1830000:
	stopnpctimer;
	end;
}

bat_a02,1,8,3	script	time_event#bat_a02	CLEAR_NPC,{
OnInit:
	stopnpctimer;
	end;

OnEnable:
	set .Tierra_Timer, 0;
	stopnpctimer;
	initnpctimer;
	end;
	
OnStop:
	stopnpctimer;
	end;

OnTimer60000:
	set .Tierra_Timer, .Tierra_Timer+1;
	set .@event_timer, callfunc("bg_getarenainfo",2,"event_timer");
	if( .Tierra_Timer >= .@event_timer )
	{
		set $@TierraBG2, 2;
		if( $@TierraBG2_GuillScore > $@TierraBG2_CroixScore )
		{
			set $@TierraBG2_Victory, 1;
			mapannounce "bat_a02", "Marollo VII: The Croix Army is victorious for its performance in Battle!",bc_map,"0x99CC00";
		}
		else if( $@TierraBG2_CroixScore > $@TierraBG2_GuillScore )
		{
			set $@TierraBG2_Victory, 2;
			mapannounce "bat_a02", "Marollo VII: The Guillaume Army is victorious for its performance in Battle!",bc_map,"0x99CC00";
		}
		else {
			set $@TierraBG2_Victory, 3;
			mapannounce "bat_a02", "Marollo VII: Both armies did not accomplish the goal, this is a sign of a tie!",bc_map,"0x99CC00";
		}
		
		// Computes Ranking and awards rewards
		callfunc "bg_finish", 2, $@TierraBG2_id1, $@TierraBG2_id2, $@TierraBG2_Victory, $@TierraBG2_GuillScore, $@TierraBG2_CroixScore;
		
		donpcevent "Battle Therapist#a02_a::OnStop";
		donpcevent "Battle Therapist#a02_b::OnStop";
		donpcevent "countdown#bat_a02::OnStop";
		donpcevent "start#bat_a02_Out::OnBegin";
		enablenpc "Lieutenant#a02_a";
		enablenpc "Lieutenant#a02_b";
		bg_warp $@TierraBG2_id1,"bat_a02",50,374;
		bg_warp $@TierraBG2_id2,"bat_a02",42,16;
		stopnpctimer;
	}
	else
	{
		stopnpctimer;
		initnpctimer;
	}
	end;
}

-	script	start#bat_a02_Out	FAKE_NPC,{
	end;

OnBegin:
	initnpctimer;
	end;
	
OnStop:
	stopnpctimer;
	end;

OnTimer1000:
	mapannounce "bat_a02", "Please talk to the Lieutenant to leave the battlefield.",bc_map,"0x00ff00";
	end;

OnTimer3000:
	mapannounce "bat_a02", "In 30 seconds, the Lieutenant will dispense with the grouping.",bc_map,"0x00ff00";
	end;

OnTimer5000:
	mapannounce "bat_a02", "Speak to the Lieutenant to leave the battlefield immediately. Please be careful.",bc_map,"0x00ff00";
	end;

OnTimer55000:
	mapannounce "bat_a02", "You will be sent back.",bc_map,"0x00ff00";
	end;

OnTimer60000:
	stopnpctimer;
	set $@TierraBG2_GuillScore,0;
	set $@TierraBG2_CroixScore,0;
	set $@TierraBG2_Victory, 0;
	bg_kick_team $@TierraBG2_id1;
	bg_kick_team $@TierraBG2_id2;
	disablenpc "Lieutenant#a02_a";
	disablenpc "Lieutenant#a02_b";
	mapwarp "bat_a02","bat_room",154,150;
	maprespawnguildid "bat_a02",0,3; // Just in case someone else
	bg_updatescore "bat_a02",0,0;
	end;
}

/*
bat_a02,351,75,3	script	Croix Camp Soldier#bat_a02_guide	4_M_RASWORD,{
	mes "Loading...";
	close;
}

bat_a02,356,326,3	script	Guillaume Camp Soldier#bat_a02_guide	4_M_RASWORD,{
	mes "Loading...";
	close;
}
*/

bat_a02,1,1,3	script	Release all#a02	4_DOG01,{
	set .@i, callfunc("F_GM_NPC",1854,0);
	if (.@i == -1) {
		mes "Cancelled.";
		close;
	} else if (.@i == 0) {
		end;
	} else {
		mes "May I help you?";
		next;
		switch(select("Release all.:Cancel.")) {
		case 1:
			mes "Bye.";
			close2;
			mapwarp "bat_a02","bat_room",154,150;
			end;
		case 2:
			mes "Cancelled.";
			close;
		}
	}
}

// Lieutenants
// -----------------------------------------------------------------
bat_a02,53,377,3	script	Lieutenant#a02_a	4_M_KY_HEAD,{
	mes "[Axl Rose]";
	if( getcharid(4) == $@TierraBG2_id2 )
	{
		mes "What?? Some of the Croix Army soldiers are doing it at Guillaume's camp!!";
		mes "A soldier will lead you to his group.";
		close2;
		warp "bat_a02", 45, 19;
		end;
	}
	else if( getcharid(4) == $@TierraBG2_id1 )
	{
		if( $@TierraBG2_Victory == 0 )
		{
			mes "The Battle is not yet soldered, go right to your post, Guillaume's Army needs you!";
			close;
		}
		else if( $@TierraBG2_Victory == 1 )
		{
			mes "Bless the Guillaume Army!";
			mes strcharinfo(0) + ", let's enjoy our glorious victory!";
		}
		else if( $@TierraBG2_Victory == 2 ) {
			mes "You lost, but you dedicated yourself to this battle for Guillaume Marollo!";
			mes "Consider this defeat as a lesson, and next time you will come out victorious.";
		}
		else {
			mes "This indeed was a great Battle!";
			mes "Not everyone can come out victorious, consider this tie as a lesson and next time we will be victorious.";
		}
		close2;
		bg_leave;
	}
	else {
		mes "A Civilian on our battlefield?";
		mes "We'll take you to a safe place!";
		close2;
	}
	warp "prontera", 156, 176;
	end;

OnInit:
	disablenpc "Lieutenant#a02_a";
	end;
}

bat_a02,45,19,3	script	Lieutenant#a02_b	4_M_CRU_HEAD,{
	mes "[Swandery]";
	if( getcharid(4) == $@TierraBG2_id1 )
	{
		mes "What?? One of Guillaume's Army soldiers is doing at Croix's camp!!";
		mes "A soldier will lead you to his group.";
		close2;
		warp "bat_a02", 45, 19;
		end;
	}
	else if( getcharid(4) == $@TierraBG2_id2 )
	{
		if( $@TierraBG2_Victory == 0 )
		{
			mes "The Battle is not yet soldered, go right to your post, the Croix Army needs you!";
			close;
		}
		else if( $@TierraBG2_Victory == 1 ) {
			mes "Oh " + strcharinfo(0) + ", do not be sed.";
			mes "Even if we did not win, we did our best.";
			mes "Do not forget this battle, we will win the next.";
		}
		else if( $@TierraBG2_Victory == 2 )
		{
			mes "Bless the Croix Army!";
			mes strcharinfo(0) + ", let's enjoy our glorious victory!";
		}
		else {
			mes "This indeed was a great Battle!";
			mes "Not everyone can come out victorious, do not forget this battle!";
			mes "We'll win the next one.";
		}
		close2;
		bg_leave;
	}
	else {
		mes "A Civilian on our battlefield?";
		mes "We'll take you to a safe place!";
		close2;
	}
	warp "prontera", 156, 176;
	end;

OnInit:
	disablenpc "Lieutenant#a02_b";
	end;
}

// MapFlags
// -----------------------------------------------------------------
bat_a02	mapflag	nowarp
bat_a02	mapflag	nowarpto
bat_a02	mapflag	nomemo
bat_a02	mapflag	noteleport
bat_a02	mapflag	nosave
bat_a02	mapflag	noreturn
bat_a02	mapflag	nobranch
bat_a02	mapflag	nopenalty
bat_a02	mapflag	battleground	2