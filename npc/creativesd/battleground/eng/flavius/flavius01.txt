/*.-----------------------------------------------------------------.
  .    ____                          __                             .
  .   /\  _`\                       /\ \__  __                      .
  .   \ \ \/\_\  _ __    __     __  \ \ ,_\/\_\  __  __     __      .
  .    \ \ \/_/_/\`'__\/'__`\ /'__`\ \ \ \/\/\ \/\ \/\ \  /'__`\    .
  .     \ \ \s\ \ \ \//\  __//\ \d\.\_\ \ \_\ \ \ \ \_/ |/\  __/    .
  .      \ \____/\ \_\\ \____\ \__/.\_\\ \__\\ \_\ \___/ \ \____\   .
  .       \/___/  \/_/ \/____/\/__/\/_/ \/__/ \/_/\/__/   \/____/   .
  .                                                                 .
  .          2014~2017 © Creative Services and Development          .
  .                      www.creativesd.com.br                      .
  .-----------------------------------------------------------------.
  . Script:                                                         .
  .    Battleground Warfare (1st Flavius Arena)                     .
  .-----------------------------------------------------------------.
  . Author: Romulo SM (sbk_)                         Version: 1.0   .
  .                                                                 .
  . Compatible:                                                     .
  .    eAthena, rAthena and derivations.                            .
  .-----------------------------------------------------------------.
  . Description:                                                    .
  .    Battle in which players must destroy the opponent's crystal. .                                                   .
  *-----------------------------------------------------------------*/
  
// Flavius Engine
// -----------------------------------------------------------------
bat_b01,15,15,3	script	start#bat_b01	CLEAR_NPC,{
	end;
	
OnInit:
	// Removing Players from Arena
	mapwarp "bat_b01","bat_room",154,150;
	
	// Creating Queue and specific times for Arena
	queue_create(3, "1st Flavius Arena", 0, 0, "start#bat_b01::OnReadyCheck");
	set $@FlaviusBG1_id1, bg_create_team(5, "bat_b01", 10, 290, 87, 75, 0, "start#bat_b01::OnGuillaumeQuit", "", "start#bat_b01::OnGuillWO");
	set $@FlaviusBG1_id2, bg_create_team(6, "bat_b01", 390, 10, 311, 224, 1, "start#bat_b01::OnCroixQuit", "", "start#bat_b01::OnCroixWO");
	
	// Expeling Players
	bg_kick_team($@FlaviusBG1_id1, 0);
	bg_kick_team($@FlaviusBG1_id2, 0);
	
	// Getting Started
	set .global_announce, 0;
	set .BlockStatus, 0;
	donpcevent "start#bat_b01::OnReadyCheck";
	
	// Set NPC's of Flags with the Symbol of the Army.
	donpcevent "::OnBatB01Emblem";
	end;

OnReadyCheck:
	if( .BlockStatus )
	{
		sleep 5000;
		donpcevent "start#bat_b01::OnReadyCheck";
	}
		
	set .BlockStatus, 1;

	set .@min_player, callfunc("bg_getarenainfo", 3, "min_player");
	set .@max_player, callfunc("bg_getarenainfo", 3, "max_player");
	set .@a_name$, callfunc("bg_getarenainfo", 3, "arena_name");
	set .@min_player, (.@min_player*2);
	
	if( .@max_player > MAX_BG_MEMBERS )
	{
		bg_console "(BG_ID: 3) Maximum number of players in a team exceeds the established limit, setting the maximum number of players to " + MAX_BG_MEMBERS + ".";
		set .@max_player, MAX_BG_MEMBERS;
	}
	
	queue_get_players 3;
	set .@playlist_team1, bg_team_size($@FlaviusBG1_id1);
	set .@playlist_team2, bg_team_size($@FlaviusBG1_id2);
	if( !.@queue_count )
	{
		set .BlockStatus, 0;
		end;
	}
	else if( $@FlaviusBG1 && $@FlaviusBG1 < 2 && .@queue_count && (.@playlist_team1 < .@max_player || .@playlist_team2 < .@max_player) )
	{
		set .@guill, 0;
		set .@croix, 0;
		set .@count_g, 0;
		set .@count_c, 0;
		
		// ---------------------------------------------------
		// -              Equating the Armies                -
		// ---------------------------------------------------
		if( .@playlist_team1 < .@playlist_team2 )
		{
			set .@guill, .@playlist_team2-.@playlist_team1;
			for( set .@i,0; .@i < .@guill; set .@i, .@i + 1 )
			{
				bg_join_team $@FlaviusBG1_id1, 1, .@queue_aid[.@i];
				queue_leave 0, .@queue_aid[.@i];
				set .@count_g, .@count_g + 1;
				message .@queue_player$[.@i], "You joined Guillaume's Army in a battle on " + .@a_name$ + " that is already happening, and will be teleported in 10 seconds.";
			}
			
			deletearray .@queue_cid;
			deletearray .@queue_aid;
			deletearray .@queue_player$;
			set .@queue_count, 0;
			queue_get_players 3; // Reset
			set .@playlist_team1, bg_team_size($@FlaviusBG1_id1);
		}
		else if( .@playlist_team2 < .@playlist_team1 )
		{
			set .@croix, .@playlist_team1-.@playlist_team2;
			for( set .@i,0; .@i < .@croix; set .@i, .@i + 1 )
			{
				bg_join_team $@FlaviusBG2_id1, 1, .@queue_aid[.@i];
				queue_leave 0, .@queue_aid[.@i];
				set .@count_c, .@count_c + 1;
				message .@queue_player$[.@i], "You joined Croix's Army in a battle on" + .@a_name$ + " that is already happening, and will be teleported in 10 seconds.";
			}
			
			deletearray .@queue_cid;
			deletearray .@queue_aid;
			deletearray .@queue_player$;
			set .@queue_count, 0;
			queue_get_players 3; // Reset
			set .@playlist_team2, bg_team_size($@FlaviusBG1_id2);
		}
		// ---------------------------------------------------
		// -             Expanding the Armies                -
		// ---------------------------------------------------
		if( .@queue_count && .@playlist_team1 == .@playlist_team2 && .@playlist_team1 < .@max_player && .@playlist_team2 < .@max_player )
		{
			set .@guill, .@max_player-.@playlist_team1;
			set .@croix, .@max_player-.@playlist_team2;
			
			if( .@queue_count > (.@guill+.@croix) )
				set .@queue_count, (.@guill+.@croix);
				
			if( .@queue_count%2 )
				set .@queue_count, .@queue_count-1;
			
			set .@size, .@queue_count/2;
			for( set .@i, 0; .@i < .@size; set .@i, .@i + 1 )
			{
				bg_join_team $@FlaviusBG1_id1, 1, .@queue_aid[.@i];
				queue_leave 0, .@queue_aid[.@i];
				set .@players_name_g$[.@count_g], .@queue_player$[.@i];
				set .@count_g, .@count_g + 1;
			}
			
			for( set .@i,.@size; .@i < .@queue_count; set .@i, .@i + 1 )
			{
				bg_join_team $@FlaviusBG1_id2, 1, .@queue_aid[.@i];
				queue_leave 0, .@queue_aid[.@i];
				set .@players_name_c$[.@count_c], .@queue_player$[.@i];
				set .@count_c, .@count_c + 1;
			}
		}
		
		if( .@count_c || .@count_g )
		{
			announce "[Battleground]: " + (.@count_c+.@count_g) + " players are being sent to a battle that is going on in " + .@a_name$ + ", the Armies were balanced and expanded.", bc_all, "0xFFCE00";
			sleep 3000;
		}
		
		deletearray .@queue_cid;
		deletearray .@queue_aid;
		deletearray .@queue_player$;
		set .@queue_count, 0;
		queue_get_players 3; // Reset
		
		set .@queue_size, queue_size(3);
		set .@team1_size, bg_team_size($@FlaviusBG1_id1);
		set .@team2_size, bg_team_size($@FlaviusBG1_id2);
		
		if( .@team1_size < .@max_player || .@team2_size < .@max_player )
		{
			set .@size, (2*.@max_player)-(.@team1_size+.@team2_size)-.@queue_size;
			announce "[Battleground]: There are still " + (.@size) + " spots for a battle going on in " + .@a_name$ + "!", bc_all, "0xFFCE00";
		}
	}
	else if( $@FlaviusBG1 && !.global_announce && .@queue_count >= .@min_player )
	{
		announce "[Battleground]: The battle is complete, but there is a battle going on in " + .@a_name$ + ", all players standing in the queue must wait.", bc_all, "0xFFCE00";
		set .global_announce, 1;
	}
	else if( .@queue_count < .@min_player )
		announce "[Battleground]: There are " + (.@min_player-.@queue_count) + " players left in the battle in " + .@a_name$ + "!", bc_all, "0xFFCE00";
	else
	{
		// ---------------------------------------------------
		// -                    Balancing                    -
		// ---------------------------------------------------
		if( .@queue_count > (.@max_player*2) )
			set .@queue_count, (.@max_player*2);
			
		if( .@queue_count%2 )
			set .@queue_count, .@queue_count-1;
			
		if( .@queue_count >= .@min_player )
		{
			for( set .@i,0; .@i < .@queue_count; set .@i, .@i + 2 )
			{
				// Entering Guillaume's Army
				bg_join_team $@FlaviusBG1_id1, 1, .@queue_aid[.@i];
				queue_leave 0, .@queue_aid[.@i];
				message .@queue_player$[.@i], "You joined Guillaume's Army, you will be teleported in 10 seconds.";
				
				// Entering Croix's Army
				bg_join_team $@FlaviusBG1_id2, 1, .@queue_aid[.@i+1];
				queue_leave 0, .@queue_aid[.@i+1];
				message .@queue_player$[.@i+1], "You joined the Croix's Army, you will be teleported in 10 seconds.";
			}
		}
		
		bg_refresh_patent($@FlaviusBG1_id1);
		bg_refresh_patent($@FlaviusBG1_id2);
		set .global_announce, 0;
		set $@FlaviusBG1, 1;
		announce "[Battleground]: Both Armies, are complete good luck in the battles at " + .@a_name$ + ".", bc_all, "0xFFCE00";
		set $@FlaviusBG1_CroixScore, 0;
		set $@FlaviusBG1_GuillScore, 0;
		bg_updatescore "bat_b01",0,0;
		donpcevent "start#bat_b01::OnEnable";
		donpcevent "BattleGround#queue_main::OnStopTimer";
		sleep 3000;
		initnpctimer;
		
		if( .@queue_count < (2*.@max_player) )
		{
			sleep 3000;
			set .@count, (2*.@max_player)-.@queue_count;
			announce "[Battleground]: There are still " + .@queue_count + " spots in the battles in " + .@a_name$ + "!", bc_all, "0xFFCE00";
		}
	}
	set .BlockStatus, 0;
	end;

OnEnable:
	donpcevent "OBJ#bat_b01_a::OnKill";
	donpcevent "OBJ#bat_b01_a::OnEnable";
	donpcevent "OBJ#bat_b01_b::OnKill";
	donpcevent "OBJ#bat_b01_b::OnEnable";
	donpcevent "guardian#bat_b01_a::OnKill";
	donpcevent "guardian#bat_b01_b::OnKill";
	donpcevent "guardian#bat_b01_a::OnEnable";
	donpcevent "guardian#bat_b01_b::OnEnable";
	donpcevent "cell#bat_b01_a::OnRed";
	donpcevent "cell#bat_b01_b::OnRed";
	donpcevent "time#bat_b01::OnEnable";
	disablenpc "Lieutenant#b01_a";
	disablenpc "Lieutenant#b01_b";
	donpcevent "countdown#bat_b01::OnEnable";
	donpcevent "time_event#bat_b01::OnEnable";
	initnpctimer;
	end;

OnReset:
	donpcevent "OBJ#bat_b01_a::OnKill";
	donpcevent "OBJ#bat_b01_a::OnEnable";
	donpcevent "OBJ#bat_b01_b::OnKill";
	donpcevent "OBJ#bat_b01_b::OnEnable";
	donpcevent "guardian#bat_b01_a::OnKill";
	donpcevent "guardian#bat_b01_b::OnKill";
	donpcevent "guardian#bat_b01_a::OnEnable";
	donpcevent "guardian#bat_b01_b::OnEnable";
	donpcevent "cell#bat_b01_a::OnRed";
	donpcevent "cell#bat_b01_b::OnRed";
	donpcevent "time#bat_b01::OnEnable";
	disablenpc "Lieutenant#b01_a";
	disablenpc "Lieutenant#b01_b";
	bg_warp $@FlaviusBG1_id1,"bat_b01",87,75;
	bg_warp $@FlaviusBG1_id2,"bat_b01",311,224;
	end;

OnGuillaumeQuit:
	callfunc "bg_clear_score", getcharid(0);
	callfunc "bg_ranking", getcharid(0), 3, 0, 0, 1, 0, $@FlaviusBG1_CroixScore;
	donpcevent "start#bat_b01::OnReadyCheck";
	end;
	
OnCroixQuit:
	callfunc "bg_clear_score", getcharid(0);
	callfunc "bg_ranking", getcharid(0), 3, 0, 0, 1, $@FlaviusBG1_GuillScore, 0;
	donpcevent "start#bat_b01::OnReadyCheck";
	end;
	
OnGuillWO:
	if( queue_size(3) )
		end;
		
	// Sign the Victory
	set $@FlaviusBG1, 2;
	set $@FlaviusBG1_Victory, 2;
	
	// Computes Ranking and awards rewards
	callfunc "bg_finish", 3, $@FlaviusBG1_id1, $@FlaviusBG1_id2, $@FlaviusBG1_Victory, $@FlaviusBG1_GuillScore, $@FlaviusBG1_CroixScore;
	
	// Starts Ending Events
	donpcevent "Battle Therapist#b01_a::OnStop";
	donpcevent "Battle Therapist#b01_b::OnStop";
	mapannounce "bat_b01", "Lieutenant Swandery: Guillaume's cowards fled. We won this one! Wow!",bc_map,"0xFFCE00";
	enablenpc "Lieutenant#b01_a";
	enablenpc "Lieutenant#b01_b";
	donpcevent "countdown#bat_b01::OnStop";
	donpcevent "time_event#bat_b01::OnStop";
	donpcevent "start#bat_b01_Out::OnBegin";
	sleep 2000;
	bg_warp $@FlaviusBG1_id1,"bat_b01",10,290;
	bg_warp $@FlaviusBG1_id2,"bat_b01",390,10;
	end;
	
OnCroixWO:
	if( queue_size(3) )
		end;
		
	// Sign the Victory
	set $@FlaviusBG1, 2;
	set $@FlaviusBG1_Victory, 1;
	
	// Computes Ranking and awards rewards
	callfunc "bg_finish", 3, $@FlaviusBG1_id1, $@FlaviusBG1_id2, $@FlaviusBG1_Victory, $@FlaviusBG1_GuillScore, $@FlaviusBG1_CroixScore;
	
	// Starts Ending Events
	donpcevent "Battle Therapist#b01_a::OnStop";
	donpcevent "Battle Therapist#b01_b::OnStop";
	mapannounce "bat_b01", "Lieutenant Guillaume Axl Rose: Croix's cowards fled. We won this one! Wow!",bc_map,"0xFFCE00";
	enablenpc "Lieutenant#b01_a";
	enablenpc "Lieutenant#b01_b";
	donpcevent "countdown#bat_b01::OnStop";
	donpcevent "time_event#bat_b01::OnStop";
	donpcevent "start#bat_b01_Out::OnBegin";
	sleep 2000;
	bg_warp $@FlaviusBG1_id1,"bat_b01",10,290;
	bg_warp $@FlaviusBG1_id2,"bat_b01",390,10;
	end;

// Player Check in the Arena.
// -------------------------------------------------------
OnTimer60000:
	stopnpctimer;
	if( !getmapusers("bat_b01") )
	{
		set $@FlaviusBG1, 0;
		bg_kick_team $@FlaviusBG1_id1;
		bg_kick_team $@FlaviusBG1_id2;
		donpcevent "start#bat_b01_Out::OnStop";
		
		if( $@BG_MODE )
			donpcevent "BattleGround#queue_main::OnReload";
		else
			donpcevent "start#bat_b01::OnReadyCheck";
	}
	else
		initnpctimer;
	end;
}

bat_b01,1,1,3	script	OBJ#bat_b01_a	CLEAR_NPC,{
OnEnable:
	bg_monster $@FlaviusBG1_id1,"bat_b01",61,150,"Pink Crystal",1915,"","OBJ#bat_b01_a::OnMyMobDead";
	end;

OnKill:
	killmonster "bat_b01","OBJ#bat_b01_a::OnMyMobDead";
	end;

OnMyMobDead:
	if (mobcount("bat_b01","OBJ#bat_b01_a::OnMyMobDead") < 1) {
		mapannounce "bat_b01", "Guillaume's Crystal has been destroyed.",bc_map,"0xFFCE00";
		set $@FlaviusBG1_CroixScore, $@FlaviusBG1_CroixScore+1;
		set .@score_round, callfunc("bg_getarenainfo",3,"score_round");
		bg_updatescore "bat_b01",$@FlaviusBG1_GuillScore,$@FlaviusBG1_CroixScore;
		
		if( $@FlaviusBG1_CroixScore >= .@score_round )
		{
			set $@FlaviusBG1, 2;
			set $@FlaviusBG1_Victory, 2;
			enablenpc "Lieutenant#b01_a";
			enablenpc "Lieutenant#b01_b";
			donpcevent "time#bat_b01::OnStop";
			donpcevent "countdown#bat_b01::OnStop";
			donpcevent "time_event#bat_b01::OnStop";
			donpcevent "start#bat_b01_Out::OnBegin";
			mapannounce "bat_b01", "Lieutenant Swandery: Wow! We destroyed Guillaume's Army Crystal. We won!",bc_map,"0xFFCE00";
			
			// Computes Ranking and awards rewards
			callfunc "bg_finish", 3, $@FlaviusBG1_id1, $@FlaviusBG1_id2, $@FlaviusBG1_Victory, $@FlaviusBG1_GuillScore, $@FlaviusBG1_CroixScore;
			
			bg_warp $@FlaviusBG1_id1,"bat_b01",10,290;
			bg_warp $@FlaviusBG1_id2,"bat_b01",390,10;
		}
		else {
			donpcevent "time#bat_b01::OnEnable";
			donpcevent "start#bat_b01::OnReset";
			bg_warp $@FlaviusBG1_id1,"bat_b01",87,75;
			bg_warp $@FlaviusBG1_id2,"bat_b01",311,224;
			sleep 1000;
			mapannounce "bat_b01", "Lieutenant Swandery: Wow! We destroyed Guillaume's Army Crystal. We won this round!",bc_map,"0xFFCE00";
		}
	}
	end;
}

bat_b01,1,2,3	script	OBJ#bat_b01_b	CLEAR_NPC,{
OnEnable:
	bg_monster $@FlaviusBG1_id2,"bat_b01",328,150,"Blue Crystal",1914,"","OBJ#bat_b01_b::OnMyMobDead";
	end;

OnKill:
	killmonster "bat_b01","OBJ#bat_b01_b::OnMyMobDead";
	end;

OnMyMobDead:
	if (mobcount("bat_b01","OBJ#bat_b01_b::OnMyMobDead") < 1) {
		mapannounce "bat_b01", "Croix's Crystal has been destroyed.",bc_map,"0xFFCE00";
		set $@FlaviusBG1_GuillScore, $@FlaviusBG1_GuillScore+1;
		set .@score_round, callfunc("bg_getarenainfo",3,"score_round");
		bg_updatescore "bat_b01",$@FlaviusBG1_GuillScore,$@FlaviusBG1_CroixScore;
		
		if( $@FlaviusBG1_GuillScore >= .@score_round )
		{
			set $@FlaviusBG1, 2;
			set $@FlaviusBG1_Victory, 1;
			enablenpc "Lieutenant#b01_a";
			enablenpc "Lieutenant#b01_b";
			donpcevent "time#bat_b01::OnStop";
			donpcevent "countdown#bat_b01::OnStop";
			donpcevent "time_event#bat_b01::OnStop";
			donpcevent "start#bat_b01_Out::OnBegin";
			mapannounce "bat_b01", "Lieutenant Axl Rose: Wow! We destroyed Croix's Army Crystal. We won!",bc_map,"0xFFCE00";
			
			// Computes Ranking and awards rewards
			callfunc "bg_finish", 3, $@FlaviusBG1_id1, $@FlaviusBG1_id2, $@FlaviusBG1_Victory, $@FlaviusBG1_GuillScore, $@FlaviusBG1_CroixScore;
			
			bg_warp $@FlaviusBG1_id1,"bat_b01",10,290;
			bg_warp $@FlaviusBG1_id2,"bat_b01",390,10;
		}
		else {
			donpcevent "time#bat_b01::OnEnable";
			donpcevent "start#bat_b01::OnReset";
			bg_warp $@FlaviusBG1_id1,"bat_b01",87,75;
			bg_warp $@FlaviusBG1_id2,"bat_b01",311,224;
			sleep 1000;
			mapannounce "bat_b01", "Lieutenant Axl Rose: Wow! We destroyed Croix's Army Crystal. We won this round!",bc_map,"0xFFCE00";
		}
	}
	end;
}

bat_b01,1,3,3	script	guardian#bat_b01_a	CLEAR_NPC,{
OnEnable:
	bg_monster $@FlaviusBG1_id1,"bat_b01",108,159,"Guardian",1949,"Camp Guard","guardian#bat_b01_a::OnMyMobDead";
	bg_monster $@FlaviusBG1_id1,"bat_b01",108,141,"Guardian",1949,"Camp Guard","guardian#bat_b01_a::OnMyMobDead";
	end;

OnKill:
	killmonster "bat_b01","guardian#bat_b01_a::OnMyMobDead";
	end;

OnMyMobDead:
	if (mobcount("bat_b01","guardian#bat_b01_a::OnMyMobDead") < 1) {
		donpcevent "cell#bat_b01_a::OnGreen";
		mapannounce "bat_b01", "The Guardian protecting Guillaume's Crystal has been slain.",bc_map,"0xFFCE00";
	}
	end;
}

bat_b01,1,3,3	script	guardian#bat_b01_b	CLEAR_NPC,{
OnEnable:
	bg_monster $@FlaviusBG1_id2,"bat_b01",307,160,"Guardian",1949,"Camp Guard","guardian#bat_b01_b::OnMyMobDead";
	bg_monster $@FlaviusBG1_id2,"bat_b01",307,138,"Guardian",1949,"Camp Guard","guardian#bat_b01_b::OnMyMobDead";
	end;

OnKill:
	killmonster "bat_b01","guardian#bat_b01_b::OnMyMobDead";
	end;

OnMyMobDead:
	if (mobcount("bat_b01","guardian#bat_b01_b::OnMyMobDead") < 1) {
		donpcevent "cell#bat_b01_b::OnGreen";
		mapannounce "bat_b01", "The Guardian protecting Croix's Crystal has been slain.",bc_map,"0xFFCE00";
	}
	end;
}

bat_b01,1,4,3	script	cell#bat_b01_a	CLEAR_NPC,{
OnRed:
	setwall "bat_b01",62,151,3,2,0,"bat_b01_a01";
	setwall "bat_b01",62,150,3,2,0,"bat_b01_a02";
	setwall "bat_b01",62,149,3,2,0,"bat_b01_a03";
	end;

OnGreen:
	delwall "bat_b01_a01";
	delwall "bat_b01_a02";
	delwall "bat_b01_a03";
	end;
}

bat_b01,1,5,3	script	cell#bat_b01_b	CLEAR_NPC,{
OnRed:
	setwall "bat_b01",329,151,3,2,0,"bat_b01_b01";
	setwall "bat_b01",329,150,3,2,0,"bat_b01_b02";
	setwall "bat_b01",329,149,3,2,0,"bat_b01_b03";
	end;

OnGreen:
	delwall "bat_b01_b01";
	delwall "bat_b01_b02";
	delwall "bat_b01_b03";
	end;
}

bat_b01,1,6,1	script	time#bat_b01	CLEAR_NPC,{
OnEnable:
	donpcevent "Battle Therapist#b01_a::OnEnable";
	donpcevent "Battle Therapist#b01_b::OnEnable";
	end;

OnStop:
	donpcevent "Battle Therapist#b01_a::OnStop";
	donpcevent "Battle Therapist#b01_b::OnStop";
	end;
}

bat_b01,10,294,3	script	Battle Therapist#b01_a	4_F_SISTER,{
	specialeffect2 EF_HEAL;
	mes "[Battle Therapist]";
	mes "Just close your eyes";
	mes "and take a deep breath.";
	mes "You can free yourself from pain.";
	close;

OnTimer25000:
	specialeffect EF_SANCTUARY;
	end;
	
OnTimer26500:
	stopnpctimer;
	donpcevent "Battle Therapist#b01_a::OnEnable";
	end;

OnEnable:
	initnpctimer;
	enablenpc "Battle Therapist#b01_a";
	end;

OnStop:
	disablenpc "Battle Therapist#b01_a";
	stopnpctimer;
	end;
}

bat_b01,389,14,3	script	Battle Therapist#b01_b	4_F_SISTER,{
	specialeffect2 EF_HEAL;
	mes "[Battle Therapist]";
	mes "Just close your eyes";
	mes "and take a deep breath.";
	mes "You can free yourself from pain.";
	close;

OnTimer25000:
	specialeffect EF_SANCTUARY;
	end;

OnTimer26500:
	stopnpctimer;
	donpcevent "Battle Therapist#b01_b::OnEnable";
	end;

OnEnable:
	initnpctimer;
	enablenpc "Battle Therapist#b01_b";
	end;

OnStop:
	disablenpc "Battle Therapist#b01_b";
	stopnpctimer;
	end;
}

bat_b01,1,5,3	script	countdown#bat_b01	CLEAR_NPC,{
OnInit:
	stopnpctimer;
	end;

OnEnable:
	stopnpctimer;
	initnpctimer;
	end;

OnStop:
	stopnpctimer;
	end;

OnTimer7000:
	mapannounce "bat_b01", "Lieutenant Axl Rose : Let's attack to destroy that Crystal!",bc_map,"0xFF9900";
	end;

OnTimer8000:
	mapannounce "bat_b01", "Lieutenant Croix Swandery : Even though Guillaume is struggling to win against us, the victory is ours. Let's show them our power.",bc_map,"0xFF99CC";
	end;

OnTimer1800000:
	mapannounce "bat_b01", "Marollo VII : Guillaume Marollo, Croix Marollo! And their followers!",bc_map,"0x99CC00";
	end;

OnTimer1803000:
	mapannounce "bat_b01", "Marollo VII : Both camps are competitive, so it's hard to judge which team is superior.",bc_map,"0x99CC00";
	end;

OnTimer1808000:
	mapannounce "bat_b01", "Marollo VII : This battle of Flavian is such a waste of time. I will decide victory and defeat by your progress.",bc_map,"0x99CC00";
	end;

OnTimer1822000:
	mapannounce "bat_b01", "Marollo VII : If you can't accept the results, try again in another valley battle!",bc_map,"0x99CC00";
	end;

OnTimer1825000:
	mapannounce "bat_b01", "Axl Rose, Swandery : Yes, sir.",bc_map,"0x99CC00";
	stopnpctimer;
	end;
}

bat_b01,1,8,3	script	time_event#bat_b01	CLEAR_NPC,{
OnInit:
	stopnpctimer;
	end;

OnEnable:
	set .Flavius_Timer, 0;
	stopnpctimer;
	initnpctimer;
	end;

OnStop:
	stopnpctimer;
	end;
	
OnTimer60000:
	set .Flavius_Timer, .Flavius_Timer+1;
	set .@event_timer, callfunc("bg_getarenainfo", 3, "event_timer");
	if( .Flavius_Timer >= .@event_timer )
	{
		set $@FlaviusBG1, 2;
		if( $@FlaviusBG1_CroixScore > $@FlaviusBG1_GuillScore )
		{
			mapannounce "bat_b01", "Marollo VII: The Croix Army is victorious for its performance in Battle!",bc_map,"0x99CC00";
			set $@FlaviusBG1_Victory, 2;
		}
		else if( $@FlaviusBG1_GuillScore > $@FlaviusBG1_CroixScore )
		{
			mapannounce "bat_b01", "Marollo VII: The Guillaume Army is victorious for its performance in Battle!",bc_map,"0x99CC00";
			set $@FlaviusBG1_Victory, 1;
		}
		else
		{
			mapannounce "bat_b01", "Marollo VII: Both armies did not accomplish the goal, this is a sign of a tie!",bc_map,"0x99CC00";
			set $@FlaviusBG1_Victory, 3;
		}
		
		// Computes Ranking and awards rewards
		callfunc "bg_finish", 3, $@FlaviusBG1_id1, $@FlaviusBG1_id2, $@FlaviusBG1_Victory, $@FlaviusBG1_GuillScore, $@FlaviusBG1_CroixScore;
		
		donpcevent "time#bat_b01::OnStop";
		donpcevent "countdown#bat_b01::OnStop";
		donpcevent "start#bat_b01_Out::OnBegin";
		enablenpc "Lieutenant#b01_a";
		enablenpc "Lieutenant#b01_b";
		bg_warp $@FlaviusBG1_id1,"bat_b01",10,290;
		bg_warp $@FlaviusBG1_id2,"bat_b01",390,10;
		stopnpctimer;
	}
	else
	{
		stopnpctimer;
		initnpctimer;
	}
	end;
}

-	script	start#bat_b01_Out	FAKE_NPC,{
	end;

OnBegin:
	initnpctimer;
	end;
	
OnStop:
	stopnpctimer;
	end;

OnTimer1000:
	mapannounce "bat_b01", "Please talk to the Lieutenant to leave the battlefield.",bc_map,"0x00ff00";
	end;

OnTimer3000:
	mapannounce "bat_b01", "In 30 seconds, the Lieutenant will dispense with the grouping.",bc_map,"0x00ff00";
	end;

OnTimer5000:
	mapannounce "bat_b01", "Speak to the Lieutenant to leave the battlefield immediately. Please be careful.",bc_map,"0x00ff00";
	end;

OnTimer55000:
	mapannounce "bat_b01", "You will be sent back.",bc_map,"0x00ff00";
	end;

OnTimer60000:
	stopnpctimer;
	set $@FlaviusBG1_GuillScore,0;
	set $@FlaviusBG1_CroixScore,0;
	set $@FlaviusBG1_Victory, 0;
	bg_kick_team $@FlaviusBG1_id1;
	bg_kick_team $@FlaviusBG1_id2;
	disablenpc "Lieutenant#b01_a";
	disablenpc "Lieutenant#b01_b";
	mapwarp "bat_b01","bat_room",154,150;
	maprespawnguildid "bat_b01",0,3; // Just in case someone else
	end;
}

// Guillaume flags
bat_b01,81,83,4	script	Guillaume Camp#flag21	GUILD_FLAG,{
	end;
	
OnBatB01Emblem:
	bg_set_npc $@FlaviusBG1_id1;
	end;
}

bat_b01,94,83,4	duplicate(Guillaume Camp#flag21)	Guillaume Camp#flag22	GUILD_FLAG
bat_b01,81,66,4	duplicate(Guillaume Camp#flag21)	Guillaume Camp#flag23	GUILD_FLAG
bat_b01,94,66,4	duplicate(Guillaume Camp#flag21)	Guillaume Camp#flag24	GUILD_FLAG
bat_b01,139,142,6	duplicate(Guillaume Camp#flag21)	Guillaume Camp#flag25	GUILD_FLAG
bat_b01,139,158,6	duplicate(Guillaume Camp#flag21)	Guillaume Camp#flag26	GUILD_FLAG
bat_b01,110,161,6	duplicate(Guillaume Camp#flag21)	Guillaume Camp#flag27	GUILD_FLAG
bat_b01,110,137,6	duplicate(Guillaume Camp#flag21)	Guillaume Camp#flag28	GUILD_FLAG
bat_b01,63,135,6	duplicate(Guillaume Camp#flag21)	Guillaume Camp#flag29	GUILD_FLAG
bat_b01,63,165,6	duplicate(Guillaume Camp#flag21)	Guillaume Camp#flag30	GUILD_FLAG
bat_b01,10,296,4	duplicate(Guillaume Camp#flag21)	Guillaume Camp#flag31	GUILD_FLAG

// Croix flags
bat_b01,306,233,4	script	Croix Camp#flag21	GUILD_FLAG,{
	end;
	
OnBatB01Emblem:
	bg_set_npc $@FlaviusBG1_id2;
	end;
}

bat_b01,317,233,4	duplicate(Croix Camp#flag21)	Croix Camp#flag22	GUILD_FLAG
bat_b01,306,216,4	duplicate(Croix Camp#flag21)	Croix Camp#flag23	GUILD_FLAG
bat_b01,317,216,4	duplicate(Croix Camp#flag21)	Croix Camp#flag24	GUILD_FLAG
bat_b01,257,158,2	duplicate(Croix Camp#flag21)	Croix Camp#flag25	GUILD_FLAG
bat_b01,257,141,2	duplicate(Croix Camp#flag21)	Croix Camp#flag26	GUILD_FLAG
bat_b01,297,164,2	duplicate(Croix Camp#flag21)	Croix Camp#flag27	GUILD_FLAG
bat_b01,297,136,2	duplicate(Croix Camp#flag21)	Croix Camp#flag28	GUILD_FLAG
bat_b01,336,161,2	duplicate(Croix Camp#flag21)	Croix Camp#flag29	GUILD_FLAG
bat_b01,336,139,2	duplicate(Croix Camp#flag21)	Croix Camp#flag30	GUILD_FLAG
bat_b01,389,16,4	duplicate(Croix Camp#flag21)	Croix Camp#flag31	GUILD_FLAG

bat_b01,1,10,3	script	Release all#b01	4_DOG01,{
	set .@i, callfunc("F_GM_NPC",1854,0);
	if (.@i == -1) {
		mes "Cancelled.";
		close;
	} else if (.@i == 0) {
		end;
	} else {
		mes "May I help you?";
		next;
		switch(select("Release all.:Cancel.")) {
		case 1:
			mes "Bye.";
			close2;
			mapwarp "bat_b01","bat_room",154,150;
			end;
		case 2:
			mes "Cancelled.";
			close;
		}
	}
}

// Lieutenants
// -----------------------------------------------------------------
bat_b01,10,294,3	script	Lieutenant#b01_a	4_M_KY_HEAD,{
	mes "[Axl Rose]";
	if( getcharid(4) == $@FlaviusBG1_id2 )
	{
		mes "What?? Some of the Croix Army soldiers are doing it at Guillaume's camp!!";
		mes "A soldier will lead you to his group.";
		close2;
		warp "bat_b01", 389, 14;
		end;
	}
	else if( getcharid(4) == $@FlaviusBG1_id1 )
	{
		if( $@FlaviusBG1_Victory == 0 )
		{
			mes "The Battle is not yet soldered, go right to your post, Guillaume's Army needs you!";
			close;
		}
		else if( $@FlaviusBG1_Victory == 1 )
		{
			mes "Bless the Guillaume Army!";
			mes strcharinfo(0) + ", let's enjoy our glorious victory!";
		}
		else if( $@FlaviusBG1_Victory == 2 ) {
			mes "You lost, but you dedicated yourself to this battle for Guillaume Marollo!";
			mes "Consider this defeat as a lesson, and next time you will come out victorious.";
		}
		else {
			mes "This indeed was a great Battle!";
			mes "Not everyone can come out victorious, consider this tie as a lesson and next time we will be victorious.";
		}
		close2;
		bg_leave;
	}
	else {
		mes "A Civilian on our battlefield?";
		mes "We'll take you to a safe place!";
		close2;
	}
	warp "prontera", 156, 176;
	end;

OnInit:
	disablenpc "Lieutenant#b01_a";
	end;
}

bat_b01,389,14,3	script	Lieutenant#b01_b	4_M_CRU_HEAD,{
	mes "[Swandery]";
	if( getcharid(4) == $@FlaviusBG1_id1 )
	{
		mes "What?? One of Guillaume's Army soldiers is doing at Croix's camp!!";
		mes "A soldier will lead you to his group.";
		close2;
		warp "bat_b01", 10, 294;
		end;
	}
	else if( getcharid(4) == $@FlaviusBG1_id2 )
	{
		if( $@FlaviusBG1_Victory == 0 )
		{
			mes "The Battle is not yet soldered, go right to your post, the Croix Army needs you!";
			close;
		}
		else if( $@FlaviusBG1_Victory == 1 ) {
			mes "Oh " + strcharinfo(0) + ", do not be sed.";
			mes "Even if we did not win, we did our best.";
			mes "Do not forget this battle, we will win the next.";
		}
		else if( $@FlaviusBG1_Victory == 2 )
		{
			mes "Bless the Croix Army!";
			mes strcharinfo(0) + ", let's enjoy our glorious victory!";
		}
		else {
			mes "This indeed was a great Battle!";
			mes "Not everyone can come out victorious, do not forget this battle!";
			mes "We'll win the next one.";
		}
		close2;
		bg_leave;
	}
	else {
		mes "A Civilian on our battlefield?";
		mes "We'll take you to a safe place!";
		close2;
	}
	warp "prontera", 156, 176;
	end;

OnInit:
	disablenpc "Lieutenant#b01_b";
	end;
}

// MapFlags
// -----------------------------------------------------------------
bat_b01	mapflag	nowarp
bat_b01	mapflag	nowarpto
bat_b01	mapflag	nomemo
bat_b01	mapflag	noteleport
bat_b01	mapflag	nosave
bat_b01	mapflag	noreturn
bat_b01	mapflag	nobranch
bat_b01	mapflag	nopenalty
bat_b01	mapflag	battleground	2