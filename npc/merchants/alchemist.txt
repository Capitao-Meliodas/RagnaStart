//===== rAthena Script ======================================= 
//= Alchemist Shop
//===== By: ================================================== 
//= rAthena Dev Team
//===== Current Version: ===================================== 
//= 1.8
//===== Compatible With: ===================================== 
//= rAthena Project
//===== Description: ========================================= 
//= Merchants for Alchemist Materials and Manuals.
//===== Additional Comments: ================================= 
//= 1.1 fixed Medicine Bowl issue, thanx 2 MasterOfMuppets
//= 1.2 Reddozen's fixes of typos. added optional Elemental
//=     Potion Guide. [Lupus]
//= 1.3 Deleted Elemental Potions Guide due to original quest [Lupus]
//= 1.4 Alchemists can now purchase 2000 Medicine Bowls at once. [SinSloth]
//= 1.5 Updated to Aegis 10.3 Standards. [L0ne_W0lf]
//= 1.6 Corrected canceling purchase. [L0ne_W0lf]
//= 1.7 Updated to match AEGIS script. [Masao]
//= 1.8 Moved Craft Book Merchant to Renewal path. [Euphy]
//============================================================ 

alde_alche,24,188,3	script	Guild Dealer	740,{
	if (checkweight(1201,1) == 0) {
		mes "Espere um pouco!";
		mes "Você está carregando";
		mes "muitos itens com você.";
		mes "Por favor, volte novamente";
		mes "depois que você guardar alguns itens no Armazém da Kafra.";
		close;
	}
	mes "[Gever Al Sharp]";
	mes "Bem-vindo à";
	mes "União dos Alquimistas.";
	mes "Como posso ajudá-lo hoje?";
	next;
	switch (select("Comprar materiais.","Comprar manuais de produção.","Cancelar.")) {
	case 1:
		mes "[Gever Al Sharp]";	
		mes "O que você gostaria de comprar?";
		next;
		if (select("Vasilha de Mistura - 8 Zeny","Cancelar.") == 1) {
			mes "[Gever Al Sharp]";
			mes "De quantas você precisa?";
			mes "Digite '0' se você deseja cancelar.";
			next;
			while(1) {
				input .@input,0,2001;
				if (.@input == 0) {
					mes "[Gever Al Sharp]";
					mes "A negociação foi cancelada.";
					mes "Volte sempre.";
					close;
				}
				else if (.@input < 1 || .@input > 2000) {
					mes "[Gever Al Sharp]";
					mes "O número deve ser";
					mes "menor que 2000.";
					next;
				}
				else {
					break;
				}
			}
			set .@sell,.@input * 8;
			set .@item_weight,.@input * 10;
			if (Zeny < .@sell) {
				mes "[Gever Al Sharp]";
				mes "Você não possui";
				mes "zeny o suficiente.";
				mes "Cheque quanto de Zeny";
				mes "você possui primeiramente.";
				close;
			}
			if ((MaxWeight - Weight) < .@item_weight) {
				mes "[Gever Al Sharp]";
				mes "Me parece que você";
				mes "não pode carregar tudo.";
				mes "Por favor, verifique o espaço";
				mes "em seu inventário.";
				close;
			}
			set Zeny, Zeny-.@sell;
			getitem 7134,.@input; //Medicine_Bowl
			mes "[Gever Al Sharp]";
			mes "Obrigado.";
			mes "Volte Sempre.";
			close;
		}
		mes "[Gever Al Sharp]";
		mes "Well then,";
		mes "come again when";
		mes "you need to purchase";
		mes "materials related to";
		mes "Alchemy, alright?";
		close;
	case 2:
		mes "[Gever Al Sharp]";
		mes "Pois bem";
		mes "volte novamente quando precisar comprar mais materiais relacionados a Alquimia, certo?";
		next;
		switch(select("Manual de Criação de Poções","Manual de Criação de Álcool","Manual de Criação de Fogo Grego","Manual de Criação de Frasco de Ácido","Manual de Criação de Plantas","Manual de Criação de Esferas-Marinhas","Manual de Criação de Revestimentos","Manual de Criação de Poções Condensadas","Cancelar Negociação.")) {
		case 1:
			callsub S_SellManual,7144,100000; //Manual de Criação de Poções
			break;
		case 2:
			callsub S_SellManual,7127,100000; //Manual de Criação de Álcool
			break;
		case 3:
			callsub S_SellManual,7128,100000; //Manual de Criação de Fogo Grego
			break;
		case 4:
			callsub S_SellManual,7129,100000; //Manual de Criação de Frasco de Ácido
			break;
		case 5:
			callsub S_SellManual,7130,100000; //Manual de Criação de Plantas
			break;
		case 6:
			callsub S_SellManual,7131,100000; //Manual de Criação de Esferas-Marinhas
			break;
		case 7:
			callsub S_SellManual,7132,100000; //Manual de Criação de Revestimentos
			break;
		case 8:
			callsub S_SellManual,7133,240000; //Manual de Criação de Poções Condensadas
			break;
		case 9:
			mes "[Gever Al Sharp]";
			mes "Pois bem...";
			mes "Volte caso você tenha";
			mes "nescessidade de comprar";
			mes "um manual de produção.";
			close;
		}
	case 3:
		mes "[Gever Al Sharp]";
		mes "Certo, então"; 
		mes "tenha um bom dia.";
		close;
	}

S_SellManual:
	.@item_id = getarg(0);
	.@zeny_req = getarg(1);

	if (Zeny < .@zeny_req) {
		mes "[Gever Al Sharp]";
		mes "Você não possui"; 
		mes "zeny o suficiente.";
		mes "Cheque quanto de zeny"; 
		mes "você possui primeiramente.";
		close;
	}
	mes "[Gever Al Sharp]";
	mes getitemname(.@item_id) +"?";
	mes "Isso vai lhe custar "+ .@zeny_req +" zeny.";
	next;
	if (select("Comprar.:Cancelar.") == 1) {
		Zeny = Zeny - .@zeny_req;
		getitem .@item_id,1;
		mes "[Gever Al Sharp]";
		mes "Obrigado pela"; 
		mes "sua fidelidade.";
		close;
	}
	mes "[Gever Al Sharp]";
	mes "Volte Sempre!";
	close;
}
