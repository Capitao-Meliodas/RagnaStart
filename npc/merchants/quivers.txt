//===== rAthena Script =======================================
//= Arrow Quiver Event
//===== By: ==================================================
//= Muad_Dib (Prometheus Project); L0ne_W0lf
//===== Current Version: =====================================
//= 1.2
//===== Compatible With: =====================================
//= rAthena Project
//===== Description: =========================================
//= [Official Conversion]
//= Turns arrows into Arrow Quivers.
//= Breakdown of Subroutine "S_BuyQuiver"
//= arg(0): Type of Arrow to be packaged (item ID).
//= arg(1): How many of each 'getarg(0)' arrow per quiver.
//= arg(2): The cost of making a 'getarg(0)' quiver.
//= arg(3): The quiver given by the NPC (item ID).
//===== Additional Comments: =================================
//= 07/06/05 : Added 1st Version. [Muad_Dib]
//= Converted to rAthena format by Dr.Evil
//= added prize to quest - 500 Zeny
//= Fixed a few spelling errors. [Nexon]
//= 1.1 Added Holy Arrow Quivers [Playtester]
//= 1.2 Rescripted to Aegis 10.3 standards. [L0ne_W0lf]
//=     Removed "Holy Arrows" from the list.
//============================================================

payon_in01,5,134,5	script	Inventor Jaax	89,{
	if (checkweight(1201,1) == 0 || MaxWeight - Weight < 2000) {
		mes "[Inventor Jaax]";
		mes "Ei, você está carregando";
		mes "muita coisa. Por que não usa o Armazém das Kafras? Podemos conversar depois de fazer isso, certo?";
		close;
	}
	mes "[Inventor Jaax]";
	mes "Meu nome é Jaax.";
	mes "Sem ego, eu posso";
	mes "Dizer que eu sou talvez o";
	mes "^663300Maior inventor do nosso tempo^000000.";
	next;
	mes "[Inventor Jaxx]";
	mes "Desta vez, eu";
	mes "criei uma coisa";
	mes "Verdadeiramente extraordinária. Elas se chamam... ^663300Aljaves Mágicas^000000 !! Isso será lembrado na história como a";
	mes "revolução das flechas!";
	emotion ET_BEST;
	next;
	mes "[Inventor Jaax]";
	mes "Eu estudei magia e aljaves durante anos, trabalhando noite e dia até que eu finalmente descobri como condensar flechas com magia! Com Aljave Mágica, você podera carregar mais flechas, e ter menos peso!";
	next;
	mes "[Inventor Jaax]";
	mes "Gostaria de tentar usar uma de minhas criações especiais? Não tenho dúvidas de que alguém como você pode apreciar a minha inteligência!";
	next;
	switch(select("Aljave:Aljave de Flechas de Ferro:Aljave de Flechas de Aço:Aljave de Flechas de Oridecon:Aljave de Flechas de Fogo:Aljave de Flechas de Prata:Aljave de Flechas de Vento:Aljave de Flechas de Pedra:Aljave de Flechas de Cristal:Aljave de Flechas de Sombra:Aljave de Flechas Imateriais:Aljave de Flechas Enferrujadas")) {
	case 1: callsub S_BuyQuiver,1750,500,500,12004;
	case 2: callsub S_BuyQuiver,1770,500,500,12005;
	case 3: callsub S_BuyQuiver,1753,500,500,12006;
	case 4: callsub S_BuyQuiver,1765,500,500,12007;
	case 5: callsub S_BuyQuiver,1752,500,500,12008;
	case 6: callsub S_BuyQuiver,1751,500,500,12009;
	case 7: callsub S_BuyQuiver,1755,500,500,12010;
	case 8: callsub S_BuyQuiver,1756,500,500,12011;
	case 9: callsub S_BuyQuiver,1754,500,500,12012;
	case 10: callsub S_BuyQuiver,1767,500,500,12013;
	case 11: callsub S_BuyQuiver,1757,500,500,12014;
	case 12: callsub S_BuyQuiver,1762,500,500,12015;
	}
	//This doesn't seem to be accessable...
	//mes "[Inventor Jaax]";
	//mes "Is there";
	//mes "anything you want?";
	//close;

S_BuyQuiver:
	.@item_id = getarg(0);
	.@amount = getarg(1);
	.@zeny_req = getarg(2);

	if (countitem(.@item_id) >= .@amount) {
		mes "[Inventor Jaax]";
		mes "Excelente!";
		mes "Você está carregando alguma Flecha com você? Eu vou lhe fornecer um aljave que pode transportar "+ .@amount +" de suas "+ getitemname(.@item_id) +"s por apenas  ^FF3131"+ .@zeny_req +" Zeny^000000.";
		next;
		switch(select("Crie quantas aljaves puder:Comprar 1 aljave:Cancelar")) {
		case 1:
			.@arrows = countitem(.@item_id);
			set .@quiver,.@arrows / .@amount;
			set .@arrows_used,.@quiver * .@amount;
			.@arrow_zeny01 = .@quiver * .@zeny_req;
			mes "Número de";
			mes "Flechas: ^3131FF"+.@arrows+" ^000000";
			mes "Número máximo";
			mes "de Aljaves";
			mes "Adquirivel: ^3131FF"+.@quiver+" ^000000";
			mes "Zenys necessários: ^3131FF"+.@arrow_zeny01+" Zenys^000000";
			next;
			mes "[Inventor Jaax]";
			mes "Você gostaria de";
			mes "Comprar o máximo de aljaves";
			mes "possiveis com o número de flechas que";
			mes "Você está atualmente carregando?";
			next;
			if (select("Sim:Cancelar") == 1) {
				if (.@arrow_zeny01 < Zeny) {
					mes "[Inventor Jaax]";
					mes "Ai está!";
					mes "Apenas lembre-se, ^FF0000Você não será capaz de usar o Aljave quando seu peso se tornar 90% do seu limite máximo^000000.";
					set Zeny, Zeny-.@arrow_zeny01;
					delitem .@item_id,.@arrows_used;// Arrow
					getitem getarg(3),.@quiver; //Quiver
					next;
					mes "[Inventor Jaax]";
					mes "Então...";
					mes "Apenas mantenha o controle";
					mes "do número de aljaves";
					mes "que você está carregando";
					mes "que você deve";
					mes "ficar bem.";
					close;
				}
				else {
					mes "[Inventor Jaax]";
					mes "Desculpe, mas você não tem Zeny suficiente. Eu não posso apenas dar estes maravilhosos materiais depois de anos de trabalho";
					mes "Para desenvolver esta revolucionária tecnologia!";
					close;
				}
			}
			mes "[Inventor Jaax]";
			mes "Você mudou de idéia?";
			mes "Quando a glória de possuir";
			mes "Uma aljava dessas está tão perto não é mesmo?";
			close;
		case 2:
			if (Zeny > .@zeny_req) {
				mes "[Inventor Jaax]";
				mes "Ai está!";
				mes "Apenas lembre-se, ^FF0000Você não será capaz de usar o Aljave quando seu peso se tornar 90% do seu limite máximo^000000.";
				Zeny = Zeny -.@zeny_req;
				delitem .@item_id,.@amount;// Arrow
				getitem getarg(3),1; //Quiver
				next;
				mes "[Inventor Jaax]";
				mes "Então...";
				mes "Apenas mantenha o controle";
				mes "do número de aljaves";
				mes "que você está carregando";
				mes "que você deve";
				mes "ficar bem.";
				close;
			}
			else {
				mes "[Inventor Jaax]";
				mes "Você nem mesmo";
				mes "possui "+ .@zeny_req +" Zeny?";
				mes "Eu sinto muito. Eu não tenho";
				mes "idéia do que dizer nessa situação...";
				mes "^333333Pobre^000000.";
				close;
			}
		case 3:
			mes "[Inventor Jaax]";
			mes "O que...?";
			mes "Você não vê que esta invenção pode mudar para sempre a maneira que as flechas são carregadas ?! O futuro é agora!";
			close;
		}
	}
	else {
		mes "[Inventor Jaax]";
		mes "Você pode levar o máximo de  "+ .@amount +" Flechas dentro desta aljave. Foi feito usando meu método secreto,";
		mes "assim que o peso total das flechas é bem diminuido.";
		next;
		mes "[Inventor Jaax]";
		mes "É um milagre da ciência! Um que você pode experimentar por si mesmo se você me trazer pelo menos "+getarg(1)+" Flechas e "+getarg(2)+" Zenys Para cada Aljave que criarei.";
		close;
	}
}
