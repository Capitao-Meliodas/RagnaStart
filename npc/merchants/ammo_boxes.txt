//===== rAthena Script =======================================
//= Magazine Dealers
//===== By: ==================================================
//= rAthena Dev Team
//===== Current Version: =====================================
//= 2.1
//===== Compatible With: =====================================
//= rAthena Project
//===== Description: =========================================
//= Turns bullets into magazines/packs.
//===== Additional Comments: =================================
//= 1.0 First version. [SinSloth]
//= 1.1 Optimized version - Reduced to only one function [SinSloth]
//= 1.2 Optimized^2, corrected npc's name [ultramage]
//= 1.2a Optimized. Please, ommit extra NPC names [Lupus]
//= 1.3 Updated to match AEGIS script. [Kisuka]
//= 1.4 Updated to match AEGIS script again. [Masao]
//= 1.5 Moved Izlude duplicate to pre-re/re paths. [Euphy]
//= 2.0 Clean-up. [Capuche]
//= 2.1 Fixes Issue #1482, where Rebellion jobs cannot use this npc. [Limestone]
//============================================================

alberta,118,157,3	script	Magazine Dealer Kenny::mdk	4_M_01,{
	mes "[Kenny]";
	if (BaseClass == Job_Gunslinger) {
		mes "Bem-vindo à minha loja de carregadores.";
		mes "Como você sabe, um número maior de projéteis podem ser carregados"; 
		mes "mais facilmente quando estão em carregadores.";
		mes "Agora, posso ajudá-lo em algo em particular?";
		next;
		switch(select("Pacote de Esferas de Relâmpago:Pacote de Esferas Cegantes:Pacote de Esferas Venenosas:Pacote de Esferas Congelantes:Pacote de Esferas de Chamas:Carregador:Carregador Sanguinário:Carregador de Prata:Cancelar")) {
		case 1: callsub S_Casing,13204,12144;// <bullet id>, <pack id>
		case 2: callsub S_Casing,13206,12145;
		case 3: callsub S_Casing,13205,12146;
		case 4: callsub S_Casing,13207,12147;
		case 5: callsub S_Casing,13203,12148;
		case 6: callsub S_Casing,13200,12149;
		case 7: callsub S_Casing,13202,12150;
		case 8: callsub S_Casing,13201,12151;
		case 9:
			mes "[Kenny]";
			mes "Bem, se você achar"; 
			mes "que tem muitos projéteis,"; 
			mes "venha me encontrar.";
			mes "É uma boa idéia"; 
			mes "armazenar projéteis em Carregadores.";
			close;
		}
	}
	mes "Bem-vindo à minha loja.";
	mes "Aqui, eu forneço"; 
	mes "Pacotes e Carregadores para Justiceiros.";
	mes "Desculpe, mas não parece que"; 
	mes "meus serviços serão úteis para você, aventureiro.";
	next;
	mes "[Kenny]";
	mes "Hehe,"; 
	mes "mas se você conhecer algum Justiceiro,"; 
	mes "mande-o para mim.";
	mes "Ninguém pode carregar tantos projéteis.";
	close;

S_Casing:
	.@bullet_id = getarg(0);
	.@pack_id = getarg(1);

	mes "[Kenny]";
	mes "Agora, você pode negociar";
	mes callfunc("F_InsertPlural",500,getitemname(.@bullet_id));
	mes "e 500 zeny por 1 "+getitemname(.@pack_id)+", então tenha certeza";
	mes "Tenha certeza que você tem projéteis o suficiente ";
	mes " e zeny para esta troca.";
	next;
	mes "[Kenny]";
	mes "Você pode trocar "+callfunc("F_InsertPlural",500,getitemname(.@bullet_id))+" e 500 zeny";
	mes "por 1 "+getitemname(.@pack_id)+".";
	next;
	mes "[Kenny]";
	mes "Lembre-se que não posso lhe dar";
	mes "mais que 50 Carregadores por vez.";
	mes "Agora digite o número de Carregadores";
	mes "que você quer receber.";
	mes "Se você deseja cancelar, digite '0'.";
	next;
	input .@amount;
	mes "[Kenny]";
	if (.@amount > 50 || .@amount == 0) {
		mes "Ei, não posso lhe dar"; 
		mes "esse número de Carregadores.";
		mes "Por favor tente novamente,"; 
		mes "e digite um número"; 
		mes "menor que 50.";
	}
	else if (countitem(.@bullet_id) >= (500*.@amount)) {
		if (Zeny >= (500*.@amount)) {
			if (checkweight(.@pack_id,.@amount) == 0) {
				mes "Ei, você tem muito lixo"; 
				mes "no seu inventário.";
				mes "Libere algum espaço"; 
				mes "e volte para trocar seus projéteis"; 
				mes "por alguns Carregadores depois, ok?";
			} else {
				mes "Certo, aqui estão"; 
				mes "seus Carregadores.";
				mes "Obrigado por visitar minha loja,"; 
				mes "e espero que use sua munição"; 
				mes "com sabedoria.";
				Zeny = Zeny -500*.@amount;
				delitem .@bullet_id,(500*.@amount);
				getitem .@pack_id,.@amount;
			}
		} else {
			mes "Desculpe, mas você"; 
			mes "não tem bastante zeny"; 
			mes "para esta troca.";
			mes "Volte a minha loja depois que você tiver"; 
			mes "um pouco mais de dinheiro.";
		}
	} else {
		mes "Desculpe, mas você"; 
		mes "não tem bastante projetéis"; 
		mes "para esta troca.";
		mes "Talvez você devesse conferir"; 
		mes "novamente seu Inventário.";
	}
	close;
}
